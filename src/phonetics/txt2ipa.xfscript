! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2020 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

! ===================================== !
! Phonological converter for Finnish !
! ===================================== !

! This file tries out conversion from Finnish orthography to a phonetic
! translation based upon IPA. It was originally copied over from Southern
! Sámi IPA rules...

define Vow       [ a | á | e | i | o | u | y | æ | ø | å | ä | ö |
                   A | Á | E | I | O | U | Y | Æ | Ø | Å | Ä | Ö |
                   é | ó | ú | í | à | è | ò | ù | ì | ë | ü | ï |
                   â | ê | ô | û | î | ã | ý |
                   É | Ó | Ú | Í | À | È | Ò | Ù | Ì | Ë | Ü | Ï |
                   Â | Ê | Ô | Û | Î | Ã | Ý |
                   ʉ | ə ] ;

define Cns [ b | c | č | d | đ | f | g | h | j | k | l | m | n | ŋ |
             B | C | Č | D | Đ | F | G | H | J | K | L | M | N | Ŋ |
             p | q | r | s | š | t | ŧ | v | w | x | z | ž |
             P | Q | R | S | Š | T | Ŧ | V | W | X | Z | Ž |
             ɟ | ʎ | ɲ | ʧ ] ;

define Sgm [ Vow | Cns ] ;

define Stop [ p | t | k | c | č | ʧ ] ;

define Alv [ t | T | d | D | ŧ | Ŧ | đ | Đ | s | S | l | L | r | R | n | N ] ;

define Son [ m | n | ŋ | ɲ | l | r | j | v | đ ] ;

define Nas [ m | M | n | N | ŋ | Ŋ | ɲ ] ;

define Syll Cns* Vow+ (ː) Cns*  ;

define down [ A -> a, Á -> á, B -> b, C -> c, Č -> č, D -> d, E -> e, F -> f,
              G -> g, H -> h, I -> i, J -> j, K -> k, L -> l, M -> m, N -> n,
              Ŋ -> ŋ, O -> o, P -> p, Q -> q, R -> r, S -> s, Š -> š, T -> t,
              U -> u, V -> v, W -> w, X -> x, Y -> y, Z -> z, Ž -> ž, Æ -> æ,
              Ø -> ø, Å -> å, Ö -> ö, Ä -> ä, Ï -> i  ] ;

define InitialAspiration p -> p ʰ , t -> t ʰ , k -> k ʰ
                         || [.#.|%-] _ ( [ l | r | v | n ] ) Vow ;

define ngGeminate g n -> ŋ ŋ ;

define ngRule n g -> ŋ ;

define ignorantDiacritics á -> a, à -> a, é -> e, è -> e, ô -> o, ñ -> n ;

define insertGS a -> ʔ a, o -> ʔ o, e -> ʔ e, i -> ʔ i, o -> ʔ o, u -> ʔ u,
                y -> ʔ y, ä -> ʔ ä, ö -> ʔ ö
                || .#. _ ;

define letter2ipa a -> ɑ , c -> k, z -> t s, x -> k s, v -> ʋ, w -> ʋ,
                  ä -> æ , ö -> ø , š -> ʃ , ž -> ʒ,
                  %^ -> %. ;


define COLONLENGTH   ɑ ɑ -> ɑ ː , e e -> e ː , i i -> i ː , o o -> o ː ,
                     u u -> u ː , y y -> y ː , ø ø -> ø ː , æ æ -> æ ː ,
                     b b -> b ː , c c -> c ː , d d -> d ː , f f -> f ː ,
                     g g -> g ː , h h -> h ː , j j -> j ː , k k -> k ː ,
                     l l -> l ː , m m -> m ː , n n -> n ː , p p -> p ː ,
                     r r -> r ː , s s -> s ː , t t -> t ː , ʃ ʃ -> ʃ ː ,
                     v v -> v ː , ʋ ʋ -> ʋ ː,
                     l̥ l̥  -> l̥ ː , r̥ r̥  -> r̥ ː , m̥ m̥ -> m̥ ː , n̥ n̥ -> n̥ ː ,
                     ŋ̊ ŋ̊ -> ŋ̊ ː , v̥ v̥ -> v̥ ː ,
                     ŋ ŋ -> ŋ ː ;

read regex [
    down
.o. InitialAspiration
.o. ngGeminate
.o. ngRule
.o. ignorantDiacritics
.o. insertGS
.o. letter2ipa
.o. COLONLENGTH
] ;


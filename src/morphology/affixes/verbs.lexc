!! !!!Verb inflection and derivation
!! The verbs' conjugation includes voice (in Finnish grammars also verbal 
!! genus),  tense/mood (tempus/modus), personal endings and negation marker
!! The verbs also have productive derivations to defective nouns as
!! infinitives, and to adjectives as participles, which are considered to be 
!! part of inflection and thus included in most versions 
!! ([VISK § 105|http://scripta.kotus.fi/visk/sisalto.php?p?105]).
!! The morphology of participles and historical 4th infinitive is further
!! detailed in section Deverbal nouns'  morphology.
!!
!! The analysis strings of verb are not as systematic as nouns,
!! as many categories collapse together in forms, e.g. the tense and mood
!! are only distinct with indicative past and non-past, otherwise mood implies
!! tense in semantic sense.

!! !!Present vowel stems

LEXICON V_BACK_WEAK_PRESENT
!!gt-norm: Verbs weak present forms
!!€ kutoa kutoa+V+Act+InfA+Sg+Lat knit
!!€ kudon kutoa+V+Act+Ind+Prt+Sg1
+Act+Ind+Prt+Sg1:n V_BACK_CLIT_OPT     ;
+Act+Ind+Prt+Sg2:t V_BACK_CLIT_OPT     ;
+Act+Ind+Prt+Pl1:mme   V_BACK_CLIT_OPT     ;
+Act+Ind+Prt+Pl2:tte   V_BACK_CLIT_OPT     ;
+Ind+Prt+ConNeg:0    V_BACK_CLIT_OPT     ;
+Act+Imprt+Sg2:0    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_WEAK_PRESENT
!!€ yltää yltää+V+Act+InfA+Sg+Lat reach
!!€ yllän yltää+V+Act+Ind+Prt+Sg1
+Act+Ind+Prt+Sg1:n V_FRONT_CLIT_OPT     ;
+Act+Ind+Prt+Sg2:t V_FRONT_CLIT_OPT     ;
+Act+Ind+Prt+Pl1:mme   V_FRONT_CLIT_OPT     ;
+Act+Ind+Prt+Pl2:tte   V_FRONT_CLIT_OPT     ;
+Ind+Prt+ConNeg:0    V_FRONT_CLIT_OPT     ;
+Act+Imprt+Sg2:0    V_FRONT_CLIT_OPT     ;

!! The strong form of present indicative endings in strong stems and ma 
!! infinitive, and maisilla derivation (nee infinitive).
LEXICON V_BACK_STRONG_PRESENT
!!€gt-norm: Verbs strong present forms
!!€ kutovat kutoa+V+Act+Ind+Prt+Pl3
!!€ kutomatta kutoa+V+Act+InfMa+Sg+Abe
+Act+Ind+Prt+Pl3:vat   V_BACK_CLIT_OPT     ;
+Act+InfMa+Sg+Abe:matta   V_BACK_POSS_AN_OPT      ;
+Act+InfMa+Sg+Ade:malla   V_BACK_CLIT_OPT     ;
+Act+InfMa+Sg+Ela:masta   V_BACK_CLIT_OPT     ;
+Act+InfMa+Sg+Ill:maan    V_BACK_CLIT_OPT     ;
+Act+InfMa+Sg+Ine:massa   V_BACK_CLIT_OPT     ;
+Act+InfMa+Sg+Ins:man    V_BACK_CLIT_OPT     ;
+Act+Der/maisilla:maisilla   V_BACK_POSS_AN_OBL        ;

LEXICON V_FRONT_STRONG_PRESENT
!!€ yltävät yltää+V+Act+Ind+Prt+Pl3
!!€ yltämättä yltää+V+Act+InfMa+Sg+Abe
+Act+Ind+Prt+Pl3:vät   V_FRONT_CLIT_OPT     ;
+Act+InfMa+Sg+Abe:mättä   V_FRONT_POSS_AN_OPT      ;
+Act+InfMa+Sg+Ade:mällä   V_FRONT_CLIT_OPT     ;
+Act+InfMa+Sg+Ela:mästä   V_FRONT_CLIT_OPT     ;
+Act+InfMa+Sg+Ill:mään    V_FRONT_CLIT_OPT     ;
+Act+InfMa+Sg+Ine:mässä   V_FRONT_CLIT_OPT     ;
+Act+InfMa+Sg+Ins:män    V_FRONT_CLIT_OPT     ;
+Act+Der/maisilla:mäisillä   V_FRONT_POSS_AN_OBL        ;

!! !Verb 3rd singular forms
!! The third singular form of present tense has few allomorphs according to
!! preceding vowel context, either lengthening or zero after long vowel stem:

LEXICON V_BACK_3SG_A
!!€ rakentaa rakentaa+V+Act+Prt+Sg3 builds
+Act+Ind+Prt+Sg3:a V_BACK_CLIT_OPT     ;

LEXICON V_BACK_3SG_E
!!€ tulee tulla+V+Act+Prt+Sg3 comes
+Act+Ind+Prt+Sg3:e V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_3SG_E
!!€ menee mennä+V+Act+Prt+Sg3 goes
+Act+Ind+Prt+Sg3:e V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_3SG_I
!!€ munii munia+V+Act+Prt+Sg3 lays eggs
+Act+Ind+Prt+Sg3:i V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_3SG_I
!!€ rämpii rämpiä+V+Act+Prt+Sg3 hikes
+Act+Ind+Prt+Sg3:i V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_3SG_O
!!€ kutoo kutoa+V+Act+Prt+Sg3
+Act+Ind+Prt+Sg3:o V_BACK_CLIT_OPT     ;

LEXICON V_BACK_3SG_U
!!€ viruu virua+V+Act+Prt+Sg3 stretches
+Act+Ind+Prt+Sg3:u V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_3SG_Y
!!€ kääntyy kääntyä+V+Act+Prt+Sg3 turns
+Act+Ind+Prt+Sg3:y V_FRONT_CLIT_OPT     ;

LEXICON V_FRONT_3SG_Ö
!!€ säilöö säilöä+V+Act+Prt+Sg3 conserves
+Act+Ind+Prt+Sg3:ö V_FRONT_CLIT_OPT     ;

LEXICON V_FRONT_3SG_Ä
!!€ vääntää vääntää+V+Act+Prt+Sg3 twist
+Act+Ind+Prt+Sg3:ä V_FRONT_CLIT_OPT     ;

!! !Past forms

LEXICON V_BACK_WEAK_PAST
!!€gt-norm: Verb past forms
!!€ kudoin kutoa+V+Act+Ind+Pst+Sg1
+Act+Ind+Pst+Sg1:in   V_BACK_CLIT_OPT     ;
+Act+Ind+Pst+Sg2:it   V_BACK_CLIT_OPT     ;
+Act+Ind+Pst+Pl1:imme V_BACK_CLIT_OPT     ;
+Act+Ind+Pst+Pl2:itte V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_WEAK_PAST
!!€ ylsin yltää+V+Act+Ind+Pst+Sg1
+Act+Ind+Pst+Sg1:in   V_FRONT_CLIT_OPT     ;
+Act+Ind+Pst+Sg2:it   V_FRONT_CLIT_OPT     ;
+Act+Ind+Pst+Pl1:imme V_FRONT_CLIT_OPT     ;
+Act+Ind+Pst+Pl2:itte V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_STRONG_PAST
!!€ kutoivat kutoa+V+Act+Ind+Pst+Pl3
+Act+Ind+Pst+Sg3:i    V_BACK_CLIT_OPT     ;
+Act+Ind+Pst+Pl3:ivat  V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_STRONG_PAST
!!€ ylsivät yltää+V+Act+Ind+Pst+Pl3
+Act+Ind+Pst+Pl3:ivät  V_FRONT_CLIT_OPT     ;
+Act+Ind+Pst+Sg3:i    V_FRONT_CLIT_OPT     ;

!! !Imperatives

LEXICON V_BACK_IMPERATIVE
!!€gt-norm: Verb imperatives
!!€ kutokaa kutoa+V+Act+Imprt+Pl2
+Act+Imprt+Pl1:kaamme   V_BACK_CLIT_OPT     ;
+Act+Imprt+Pl2+Foc/s:kaas  V_BACK_CLIT_OPT     ;
+Act+Imprt+Pl2:kaa  V_BACK_CLIT_OPT     ;
+Act+Imprt+Pl3:koot V_BACK_CLIT_OPT     ;
+Act+Imprt+Sg3:kon V_BACK_CLIT_OPT     ;
+Imprt+ConNeg:ko   V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_IMPERATIVE
!!€ yltäkää yltää+V+Act+Imprt+Pl2
+Act+Imprt+Pl1:käämme   V_FRONT_CLIT_OPT     ;
+Act+Imprt+Pl2+Foc/s:kääs  V_FRONT_CLIT_OPT     ;
+Act+Imprt+Pl2:kää  V_FRONT_CLIT_OPT     ;
+Act+Imprt+Pl3:kööt V_FRONT_CLIT_OPT     ;
+Act+Imprt+Sg3:kön V_FRONT_CLIT_OPT     ;
+Imprt+ConNeg:kö   V_FRONT_CLIT_OPT     ;

!! !Conditionals

LEXICON V_BACK_CONDITIONAL
!!€gt-norm: Verb conditionals
!!€ kutoisin kutoa+V+Act+Cond+Sg1
+Act+Cond+Pl1:isimme V_BACK_CLIT_OPT     ;
+Act+Cond+Pl2:isitte V_BACK_CLIT_OPT     ;
+Act+Cond+Pl3:isivat  V_BACK_CLIT_OPT     ;
+Act+Cond+Sg1:isin   V_BACK_CLIT_OPT     ;
+Act+Cond+Sg2:isit   V_BACK_CLIT_OPT     ;
+Act+Cond+Sg3:isi    V_BACK_CLIT_OPT     ;
+Cond+ConNeg:isi V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_CONDITIONAL
!!€ yltäisin yltää+V+Act+Cond+Sg1
+Act+Cond+Pl1:isimme V_FRONT_CLIT_OPT     ;
+Act+Cond+Pl2:isitte V_FRONT_CLIT_OPT     ;
+Act+Cond+Pl3:isivät  V_FRONT_CLIT_OPT     ;
+Act+Cond+Sg1:isin   V_FRONT_CLIT_OPT     ;
+Act+Cond+Sg2:isit   V_FRONT_CLIT_OPT     ;
+Act+Cond+Sg3:isi    V_FRONT_CLIT_OPT     ;
+Cond+ConNeg:isi V_FRONT_CLIT_OPT     ;

!! !Potentials

LEXICON V_BACK_POTENTIAL_N
!!€gt-norm: Verb potentials
!!€ kutonen kutoa+V+Act+Pot+Sg1
+Act+Pot+Pl1:nemme V_BACK_CLIT_OPT     ;
+Act+Pot+Pl2:nette V_BACK_CLIT_OPT     ;
+Act+Pot+Pl3:nevat  V_BACK_CLIT_OPT     ;
+Act+Pot+Sg1:nen   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg2:net   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg3:nee   V_BACK_CLIT_OPT     ;
+Pot+ConNeg:ne V_BACK_CLIT_OPT     ;
+Act+PrfPrc:n   A_KULUNUT       ;
+Act+Ind+Pst+ConNeg:nut  V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_POTENTIAL_N
!!€ yltänen yltää+V+Act+Pot+Sg1
+Act+Pot+Pl1:nemme V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl2:nette V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl3:nevät  V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg1:nen   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg2:net   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg3:nee   V_FRONT_CLIT_OPT     ;
+Pot+ConNeg:ne V_FRONT_CLIT_OPT     ;
+Act+PrfPrc:n   A_ÄLLISTYNYT     ;
+Act+Ind+Pst+ConNeg:nyt  V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_POTENTIAL_S
!!€ juossen juosta+V+Act+Pot+Sg1 I might run
+Act+Pot+Pl1:semme V_BACK_CLIT_OPT     ;
+Act+Pot+Pl2:sette V_BACK_CLIT_OPT     ;
+Act+Pot+Pl3:sevat  V_BACK_CLIT_OPT     ;
+Act+Pot+Sg1:sen   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg2:set   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg3:see   V_BACK_CLIT_OPT     ;
+Pot+ConNeg:se V_BACK_CLIT_OPT     ;
+Act+PrfPrc:s   A_KULUNUT       ;
+Act+Ind+Pst+ConNeg:sut  V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_POTENTIAL_S
!!€ päässen päästä+V+Act+Pot+Sg1 I might be able to
+Act+Pot+Pl1:semme V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl2:sette V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl3:sevät  V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg1:sen   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg2:set   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg3:see   V_FRONT_CLIT_OPT     ;
+Pot+ConNeg:se V_FRONT_CLIT_OPT     ;
+Act+PrfPrc:s A_ÄLLISTYNYT      ;
+Act+Ind+Pst+ConNeg:syt  V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_POTENTIAL_L
!!€ tullen tulla+V+Act+Pot+Sg1
+Act+Pot+Pl1:lemme V_BACK_CLIT_OPT     ;
+Act+Pot+Pl2:lette V_BACK_CLIT_OPT     ;
+Act+Pot+Pl3:levat  V_BACK_CLIT_OPT     ;
+Act+Pot+Sg1:len   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg2:let   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg3:lee   V_BACK_CLIT_OPT     ;
+Pot+ConNeg:le V_BACK_CLIT_OPT     ;
+Act+PrfPrc:l  A_KULUNUT      ;
+Act+Ind+Pst+ConNeg:lut  V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_POTENTIAL_L
!!€ kävellen kävellä+V+Act+Pot+Sg1 I might walk
+Act+Pot+Pl1:lemme V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl2:lette V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl3:levät  V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg1:len   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg2:let   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg3:lee   V_FRONT_CLIT_OPT     ;
+Pot+ConNeg:le V_FRONT_CLIT_OPT     ;
+Act+PrfPrc:l  A_ÄLLISTYNYT  ;
+Act+Ind+Pst+ConNeg:lyt  V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_POTENTIAL_R
!!€ surren surra+V+Act+Pot+Sg1 I might be sad
+Act+Pot+Pl1:remme V_BACK_CLIT_OPT     ;
+Act+Pot+Pl2:rette V_BACK_CLIT_OPT     ;
+Act+Pot+Pl3:revat  V_BACK_CLIT_OPT     ;
+Act+Pot+Sg1:ren   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg2:ret   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg3:ree   V_BACK_CLIT_OPT     ;
+Pot+ConNeg:re V_BACK_CLIT_OPT     ;
+Act+PrfPrc:r  A_KULUNUT      ;
+Act+Ind+Pst+ConNeg:rut  V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_POTENTIAL_R
!!€ pierren pierrä+V+Act+Pot+Sg1 I might fart
+Act+Pot+Pl1:remme V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl2:rette V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl3:revät  V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg1:ren   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg2:ret   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg3:ree   V_FRONT_CLIT_OPT     ;
+Pot+ConNeg:re V_FRONT_CLIT_OPT     ;
+Act+PrfPrc:r  A_ÄLLISTYNYT  ;
+Act+Ind+Pst+ConNeg:ryt  V_FRONT_CLIT_OPT     ;

!! !!Passive forms
!! The passive forms usually contain -ta-, -tä-, -da-, -dä-, element in them.
!! The variation between the realisations is one key factor of determining the
!! classification of the verb roots.

LEXICON V_BACK_PASSIVE
!!€gt-norm: Verb passives
!!€ kudottiin kutoa+V+Pss+Ind+Pst+Pe4
!!€ juostiin juosta+V+Pss+Ind+Pst+Pe4
+Pss+Cond+Pe4:taisiin V_BACK_CLIT_OPT     ;
+Pss+Cond+Pe4+ConNeg:taisi  V_BACK_CLIT_OPT     ;
+Pss+Imprt+Pe4+ConNeg:tako    V_BACK_CLIT_OPT     ;
+Pss+Imprt+Pe4:takoon    V_BACK_CLIT_OPT     ;
+Pss+InfE+Ine:taessa  V_BACK_CLIT_OPT      ;
+Pss+InfE+Ins:taen   V_BACK_CLIT_OPT     ;
+Pss+InfMa+Sg+Ins:taman  V_BACK_CLIT_OPT     ;
+Pss+Pot+Pe4:taneen  V_BACK_CLIT_OPT     ;
+Pss+Pot+Pe4+ConNeg:tane   V_BACK_CLIT_OPT     ;
+Pss+Ind+Pst+Pe4:tiin V_BACK_CLIT_OPT     ;
+Pss+PrsPrc:tav    N_SOITTAJA   ;

LEXICON V_FRONT_PASSIVE
!!€ nähtiin nähdä+V+Pss+Ind+Pst+Pe4
+Pss+Cond+Pe4:täisiin V_FRONT_CLIT_OPT     ;
+Pss+Cond+Pe4+ConNeg:täisi  V_FRONT_CLIT_OPT     ;
+Pss+Imprt+Pe4+ConNeg:täkö    V_FRONT_CLIT_OPT     ;
+Pss+Imprt+Pe4:täköön    V_FRONT_CLIT_OPT     ;
+Pss+InfE+Ine:täessa  V_FRONT_CLIT_OPT      ;
+Pss+InfE+Ins:täen   V_FRONT_CLIT_OPT     ;
+Pss+InfMa+Ins:tämän  V_FRONT_CLIT_OPT     ;
+Pss+Pot+Pe4:täneen  V_FRONT_CLIT_OPT     ;
+Pss+Pot+Pe4+ConNeg:täne   V_FRONT_CLIT_OPT     ;
+Pss+Ind+Pst+Pe4:tiin V_FRONT_CLIT_OPT     ;
+Pss+PrsPrc:täv    A_TYÖLLISTETTÄVÄ ;


!! The form of present passive assimilates leftwards, varying between
!! -ta, -tä, -da, -dä, -la, -lä, -ra, -rä, -na, -nä.

LEXICON V_BACK_PASSIVE_T
!!€ juostaan juosta+V+Pss+Ind+Prt+Pe4
+Pss+Ind+Prt+Pe4:taan    V_BACK_CLIT_OPT     ;
+Pss+Ind+Prt+Pe4+ConNeg:ta    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_PASSIVE_T
!!€ ylletään yltää+V+Pss+Ind+Prt+Pe4
+Pss+Ind+Prt+Pe4:tään    V_FRONT_CLIT_OPT     ;
+Pss+Ind+Prt+Pe4+ConNeg:tä    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_PASSIVE_D
!!€ kopioidaan kopioida+V+Pss+Ind+Prt+Pe4 copy-passive
+Pss+Ind+Prt+Pe4:daan    V_BACK_CLIT_OPT     ;
+Pss+Ind+Prt+Pe4+ConNeg:da    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_PASSIVE_D
!!€ nähdään nähdä+V+Pss+Ind+Prt+Pe4
+Pss+Ind+Prt+Pe4:dään    V_FRONT_CLIT_OPT     ;
+Pss+Ind+Prt+Pe4+ConNeg:dä    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_PASSIVE_L
!!€ vuollaan vuolla+V+Pss+Ind+Prt+Pe4 carve wood-passive
+Pss+Ind+Prt+Pe4:laan    V_BACK_CLIT_OPT     ;
+Pss+Ind+Prt+Pe4+ConNeg:la    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_PASSIVE_L
!!€ niellään niellä+V+Pss+Ind+Prt+Pe4 swallow-passive
+Pss+Ind+Prt+Pe4:lään    V_FRONT_CLIT_OPT     ;
+Pss+Ind+Prt+Pe4+ConNeg:lä    V_FRONT_CLIT_OPT     ;

LEXICON V_FRONT_PASSIVE_R
!!€ pierrään pierrä+V+Pss+Ind+Prt+Pe4 fart-passive
+Pss+Ind+Prt+Pe4:rään    V_FRONT_CLIT_OPT     ;
+Pss+Ind+Prt+Pe4+ConNeg:rä    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_PASSIVE_N
!!€ pannaan panna+V+Pss+Ind+Prt+Pe4 put-passive
+Pss+Ind+Prt+Pe4:naan    V_BACK_CLIT_OPT     ;
+Pss+Ind+Prt+Pe4+ConNeg:na    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_PASSIVE_N
!!€ mennään mennä+V+Pss+Ind+Prt+Pe4
+Pss+Ind+Prt+Pe4:nään    V_FRONT_CLIT_OPT     ;
+Pss+Ind+Prt+Pe4+ConNeg:nä    V_FRONT_CLIT_OPT     ;


!! !!Infinite verb forms

LEXICON V_BACK_AINF
!!€gt-norm: Verb a infinitives
!!€ juoda juoda+V+Act+InfA+Sg+Lat drink
+Act+InfA+Sg+Lat:a     V_BACK_CLIT_OPT     ;
+Act+InfA+Sg+Tra:akse  V_BACK_POSS_EN_OBL        ;

LEXICON V_FRONT_AINF
!!€ nähdä nähdä+V+Act+InfA+Sg+Lat
+Act+InfA+Sg+Lat:ä     V_FRONT_CLIT_OPT     ;
+Act+InfA+Sg+Tra:äkse  V_FRONT_POSS_EN_OBL        ;

LEXICON V_BACK_EINF
!!€gt-norm: Verb E infinitives
!!€ kutoessa kutoa+V+Act+InfE+Sg+Ine
+Act+InfE+Sg+Ine:essa   V_BACK_POSS_AN_OPT      ;
+Act+InfE+Sg+Ins:en    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_EINF
!!€ yltäessä yltää+V+Act+InfE+Sg+Ine
+Act+InfE+Sg+Ine:essä   V_FRONT_POSS_AN_OPT      ;
+Act+InfE+Sg+Ins:en    V_FRONT_CLIT_OPT     ;

!! !Participles

LEXICON V_BACK_TUPCP_STRONG
!!€gt-norm: Verb nut participle passives
!!€ valkaistu valkaista+V+Pss+PrfPrc+Sg+Nom bleached
+Pss+PrfPrc:tu            A_VALKAISTU        ;
+Pss+Ind+Pst+ConNeg:tu    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_TUPCP_STRONG
!!€ häpäisty häpäistä+V+Pss+PrfPrc+Sg+Nom sacrileged
+Pss+PrfPrc:ty            A_HÄPÄISTY     ;
+Pss+Ind+Pst+ConNeg:ty    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_TUPCP_0
!!€ huudettu huutaa+V+Pss+PrfPrc+Sg+Nom yelled
+Pss+PrfPrc:0             A_VIMMATTU      ;
+Pss+Ind+Pst+ConNeg:tu    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_TUPCP_0
!!€ ylennetty ylentää+V+Pss+PrfPrc+Sg+Nom promoted
+Pss+PrfPrc:0             A_YLENNETTY       ;
+Pss+Ind+Pst+ConNeg:ty    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_TUPCP_D
!!€ viipaloitu viipaloida+V+Pss+PrfPrc+Sg+Nom sliced
+Pss+PrfPrc:0             A_VIIPALOITU    ;
+Pss+Ind+Pst+ConNeg:tu    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_TUPCP_D
!!€ yksilöity yksilöidä+V+Pss+PrfPrc+Sg+Nom singled out
+Pss+PrfPrc:0             A_YKSILÖITY        ;
+Pss+Ind+Pst+ConNeg:ty    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_TUPCP_L
!!€ paranneltu parannella+V+Pss+PrfPrc+Sg+Nom embettered
+Pss+PrfPrc:0             A_PARANNELTU  ;
+Pss+Ind+Pst+ConNeg:tu    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_TUPCP_L
!!€ vähätelty vähätellä+V+Pss+PrfPrc+Sg+Nom belittled
+Pss+PrfPrc:0             A_VÄHÄTELTY        ;
+Pss+Ind+Pst+ConNeg:ty    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_TUPCP_N
!!€ pantu panna+V+Pss+PrfPrc+Sg+Nom put
+Pss+PrfPrc:0             A_PANTU  ;
+Pss+Ind+Pst+ConNeg:tu    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_TUPCP_N
!!€ menty mennä+V+Pss+PrfPrc+Sg+Nom went
+Pss+PrfPrc:0             A_MENTY        ;
+Pss+Ind+Pst+ConNeg:ty    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_TUPCP_R
!!€ purtu purra+V+Pss+PrfPrc+Sg+Nom bitten
+Pss+PrfPrc:0             A_PURTU ;
+Pss+Ind+Pst+ConNeg:tu    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_TUPCP_R
!!€ pierty pierrä+V+Pss+PrfPrc+Sg+Nom
+Pss+PrfPrc:0             A_PIERTY     ;
+Pss+Ind+Pst+ConNeg:ty    V_FRONT_CLIT_OPT     ;

!! !Possessives for infinite verb forms
LEXICON V_FRONT_POSS
!!€gt-norm: Verb possessives
!!€ kutoakseni kutoa+V+Act+InfA+Sg+Tra+PxSg1
!!€ kutoessasi kutoa+V+Act+InfE+Sg+Ine+PxSg2
+PxSg1:ni  V_FRONT_CLIT_OPT        ;
+PxSg2:si  V_FRONT_CLIT_OPT        ;
+PxSg3:nsä V_FRONT_CLIT_OPT        ;
+PxPl1:mme V_FRONT_CLIT_OPT        ;
+PxPl2:nne V_FRONT_CLIT_OPT        ;
+PxPl3:nsä V_FRONT_CLIT_OPT        ;

LEXICON V_BACK_POSS
!!€ nähdäkseni nähdä+V+Act+InfA+Sg+Tra+PxSg1
!!€ nähdessäsi nähdä+V+Act+InfE+Sg+Ine+PxSg2
+PxSg1:ni  V_BACK_CLIT_OPT        ;
+PxSg2:si  V_BACK_CLIT_OPT        ;
+PxSg3:nsa V_BACK_CLIT_OPT        ;
+PxPl1:mme V_BACK_CLIT_OPT        ;
+PxPl2:nne V_BACK_CLIT_OPT        ;
+PxPl3:nsa V_BACK_CLIT_OPT        ;

LEXICON V_BACK_POSS_AN
!!€ kutomattaan kutoa+V+Act+InfMa+Sg+Abe+PxSg3
+PxSg3:an    V_BACK_CLIT_OPT     ;
+PxPl3:an    V_BACK_CLIT_OPT     ;

LEXICON V_BACK_POSS_EN
!!€ kutoakseen kutoa+V+Act+InfA+Sg+Tra+PxPl3
+PxSg3:en    V_BACK_CLIT_OPT     ;
+PxPl3:en    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_POSS_EN
!!€ nähdäkseen nähdä+V+Act+InfA+Sg+Tra+PxPl3
+PxSg3:en    V_FRONT_CLIT_OPT     ;
+PxPl3:en    V_FRONT_CLIT_OPT     ;

LEXICON V_FRONT_POSS_AN
!!€ näkemättään nähdä+V+Act+InfMa+Sg+Abe+PxSg3
+PxSg3:än    V_FRONT_CLIT_OPT     ;
+PxPl3:än    V_FRONT_CLIT_OPT     ;

!! !Verb clitics

LEXICON V_BACK_CLIT
!!€gt-norm: Verbs clitics
!!€ kudonhan kutoa+V+Act+Ind+Prt+Sg1+Foc/han
!!€ kudotko kutoa+V+Act+Ind+Prt+Sg2+Qst
+Foc/han:han   #        ;
+Foc/han+Foc/kaan:hankaan  #        ;
+Foc/han+Qst:hanko #        ;
+Foc/han+Foc/pa:hanpa #        ;
+Qst:ko #        ;
+Qst+Foc/han:kohan #        ;
+Qst+Foc/kaan:kokaan    #        ;
+Qst+Foc/kin:kokin    #        ;
+Qst+Foc/pa:kopa   #        ;
+Qst+Foc/s:kos    #        ;
+Foc/pa:pa #        ;
+Foc/pa+Foc/han:pahan #        ;
+Foc/pa+Foc/kaan:pakaan    #        ;
+Foc/pa+Foc/kin:pakin    #        ;
+Foc/pa+Qst:pako   #        ;
+Foc/pa+Foc/s:pas    #        ;
+Foc/kin:kin  #        ;
+Foc/kin+Foc/han:kinhan  #        ;
+Foc/kin+Foc/kaan:kinkaan #        ;
+Foc/kin+Qst:kinko    #        ;
+Foc/kin+Foc/pa:kinpa    #        ;
+Foc/kaan:kaan  #        ;
+Foc/kaan+Foc/han:kaanhan  #        ;
+Foc/kaan+Foc/kin:kaankin #        ;
+Foc/kaan+Qst:kaanko    #        ;
+Foc/kaan+Foc/pa:kaanpa    #        ;

LEXICON V_FRONT_CLIT
!!€ näenhän nähdä+V+Act+Ind+Prt+Sg1+Foc/han
!!€ näetkö nähdä+V+Act+Ind+Prt+Sg2+Qst
+Foc/han:hän   #        ;
+Foc/han+Foc/kaan:hänkään  #        ;
+Foc/han+Qst:hänkö #        ;
+Foc/han+Foc/pa:hänpä #        ;
+Qst:kö #        ;
+Qst+Foc/han:köhän #        ;
+Qst+Foc/kaan:kökään    #        ;
+Qst+Foc/kin:kökin    #        ;
+Qst+Foc/pa:köpä   #        ;
+Qst+Foc/s:kös    #        ;
+Foc/pa:pä #        ;
+Foc/pa+Foc/han:pähän #        ;
+Foc/pa+Foc/kaan:päkään    #        ;
+Foc/pa+Foc/kin:päkin    #        ;
+Foc/pa+Qst:päkö   #        ;
+Foc/pa+Foc/s:päs    #        ;
+Foc/kin:kin  #        ;
+Foc/kin+Foc/han:kinhän  #        ;
+Foc/kin+Foc/kaan:kinkään #        ;
+Foc/kin+Qst:kinkö    #        ;
+Foc/kin+Foc/pa:kinpä    #        ;
+Foc/kaan:kään  #        ;
+Foc/kaan+Foc/han:käänhän  #        ;
+Foc/kaan+Foc/kin:käänkin #        ;
+Foc/kaan+Qst:käänkö    #        ;
+Foc/kaan+Foc/pa:käänpä    #        ;

! Shorthands are not tested or documented yet
LEXICON V_BACK_CLIT_OPT
0   V_BACK_CLIT    ;
0   #   ;

LEXICON V_FRONT_CLIT_OPT
0   V_FRONT_CLIT    ;
0   #   ;

LEXICON V_BACK_POSS_EN_OBL
0   V_BACK_POSS  ;
0   V_BACK_POSS_EN ;

LEXICON V_FRONT_POSS_EN_OBL
0   V_FRONT_POSS  ;
0   V_FRONT_POSS_EN ;

LEXICON V_BACK_POSS_AN_OBL
0   V_BACK_POSS  ;
0   V_BACK_POSS_AN ;

LEXICON V_BACK_POSS_OPT
0   V_BACK_POSS  ;
0   #   ;

LEXICON V_BACK_POSS_AN_OPT
0   V_BACK_POSS  ;
0   V_BACK_POSS_AN ;
0   #   ;

LEXICON V_FRONT_POSS_AN_OBL
0   V_FRONT_POSS  ;
0   V_FRONT_POSS_AN ;

LEXICON V_FRONT_POSS_OPT
0   V_FRONT_POSS  ;
0   #   ;

LEXICON V_FRONT_POSS_AN_OPT
0   V_FRONT_POSS  ;
0   V_FRONT_POSS_AN ;
0   #   ;

!! !!Deverbal derivations
!! Part of the deverbal derivation system in Finnish is so regular that it has
!! been included as part of inflectional morphology in many traditional systems.
!! These derivations are treated as inflection in our system as well.

!! !-minen, "Fourth infinitive"

LEXICON V_BACK_DERIV_MINEN
!!€gt-norm: Verbs minen derivations
!!€ aakkostaminen aakkostaa+V+Der/minen+Sg+Nom
+Der/minen:mi  N_AAKKOSTAMINEN   ;

LEXICON V_FRONT_DERIV_MINEN
!!€ näkeminen nähdä+V+Der/minen+Sg+Nom
+Der/minen:mi  N_KYLKIÄINEN   ;

!! !Participles

LEXICON V_BACK_DERIV_PCPS
!!€gt-norm: Verbs participles
!!€ kutomani kutoa+V+AgPrc+Sg+Nom+PxSg1
!!€ kutomaton kutoa+V+AgPrc+Der/ton+Sg+Nom
!!€ kutova kutoa+V+PrsPrc+Sg+Nom
+AgPrc:m       A_AALTOILEVA        ;
+AgPrc+Der/ton:mat     A_VIATON      ;
+PrsPrc:v       A_AALTOILEVA      ;
+Der/minen:mi      N_AAKKOSTAMINEN        ;
+PrsPrc+Der/inen:vai     N_AAKKOSTAMINEN  ;

LEXICON V_FRONT_DERIV_PCPS
!!€ näkemäni nähdä+V+AgPrc+Sg+Nom+PxSg1
!!€ näkemätön nähdä+V+AgPrc+Der/ton+Sg+Nom
!!€ näkevä nähdä+V+PrsPrc+Sg+Nom
+AgPrc:m       A_TYÖLLISTETTÄVÄ        ;
+AgPrc+Der/ton:mät     A_KYVYTÖN       ;
+PrsPrc:v       A_TYÖLLISTETTÄVÄ       ;
+Der/minen:mi      N_KYLKIÄINEN        ;
+PrsPrc+Der/inen:väi     N_KYLKIÄINEN  ;

!! !Other derivations
!! Not used in inflectional morphology, yet.

!LEXICON N_BACK_DERIV_VAINEN
!+PrsPrc+Der/inen:vai  N_AAKKOSTAMINEN ;

!LEXICON N_FRONT_DERIV_VÄINEN
!+PrsPrc+Der/inen:väi    N_KYLKIÄINEN   ;

!LEXICON N_BACK_DERIV_JA
!+Der/ja:j     N_MAKKARA    ;

!LEXICON N_FRONT_DERIV_JÄ
!+Der/ja:j     N_HÄKKYRÄ ;

!LEXICON N_BACK_DERIV_VUUS
!+PrsPrc+Drv/uus:vuu     N_AAKKOSELLISUUS  ;

!LEXICON N_FRONT_DERIV_VUUS
!+PrsPrc+Drv/uus:vyy   N_KÖYHYYS ;

!LEXICON V_BACK_DERIV_CAUSATIVE
!+Der/ttaa:t       V_VIEROITTAA ;
!+Der/tattaa:tat     V_VIEROITTAA ;
!+Der/tatuttaa:tatut   V_VIEROITTAA ;

!LEXICON V_FRONT_DERIV_CAUSATIVE
!+Der/ttaa:t       V_RÄPSYTTÄÄ ;
!+Der/tattaa:tat     V_RÄPSYTTÄÄ ;
!+Der/tatuttaa:tatut   V_RÄPSYTTÄÄ ;

!LEXICON V_BACK_DERIV_TUUS
!0:tuu     N_AAKKOSELLISUUS        ;

!LEXICON V_FRONT_DERIV_TYYS
!0:tyy   N_KÖYHYYS   ;

! Spoken
!LEXICON V_BACK_CONDITIONAL_DIAL
!+Act+Cond+Sg1+Qst+Dial/Standard:inks V_BACK_CLIT_OPT ;
!+Act+Cond+Sg2+Qst+Dial/Standard:iks V_BACK_CLIT_OPT ;
!+Act+Cond+Sg3+Dial/Standard:is  V_BACK_CLIT_OPT     ;

!LEXICON V_BACK_PASSIVE_DIAL
!+Pss+Cond+Pe4+ConNeg+Dial/Standard:tais    V_BACK_CLIT_OPT     ;
!+Pss+Ind+Pst+Pe4+Dial/Standard:tii   V_BACK_CLIT_OPT     ;

! Archaisms
!LEXICON V_BACK_IMPERATIVE_ARCHAIC
! The first singular imperative, the long forms of the second plural imperatives
! and the optatives are rare, poetic, archaic, etc:
!+Act+Imprt+Sg1+Use/Rare:kaani    V_BACK_CLIT_OPT     ;
!+Act+Imprt+Sg1+Use/Rare:kaami    V_BACK_CLIT_OPT     ;
!+Act+Imprt+Pl2+Use/Rare:kaatte   V_BACK_CLIT_OPT     ;
!+Act+Imprt+Pl2+Use/Rare:kaatten  V_BACK_CLIT_OPT     ;
!+Act+Optat+Sg2+Use/Rare:kootte  V_BACK_CLIT_OPT     ;
!+Act+Optat+Pl1+Use/Rare:koomme  V_BACK_CLIT_OPT     ;
!+Act+Optat+Pl2+Use/Rare:kaa  V_BACK_CLIT_OPT     ;

!LEXICON V_FRONT_IMPERATIVE_ARCHAIC
! The first singular imperative, the long forms of the second plural imperatives
! and the optatives are rare, poetic, archaic, etc:
!+Act+Imprt+Sg1+Use/Rare:kaani    V_FRONT_CLIT_OPT     ;
!+Act+Imprt+Sg1+Use/Rare:käämi    V_FRONT_CLIT_OPT     ;
!+Act+Imprt+Pl2+Use/Rare:käätte   V_FRONT_CLIT_OPT     ;
!+Act+Imprt+Pl2+Use/Rare:käätten  V_FRONT_CLIT_OPT     ;
!+Act+Optat+Sg2+Use/Rare:köötte  V_FRONT_CLIT_OPT     ;
!+Act+Optat+Pl1+Use/Rare:köömme  V_FRONT_CLIT_OPT     ;
!+Act+Optat+Pl2+Use/Rare:kää  V_FRONT_CLIT_OPT     ;

!LEXICON V_BACK_OPTATIVE_0_ARCHAIC
!+Act+Optat+Sg1+Use/Rare:on   V_BACK_CLIT_OPT     ;
!+Act+Optat+Sg2+Use/Rare:os    V_BACK_CLIT_OPT     ;

!LEXICON V_FRONT_OPTATIVE_0_ARCHAIC
!+Act+Optat+Sg1+Use/Rare:ön   V_FRONT_CLIT_OPT     ;
!+Act+Optat+Sg2+Use/Rare:ös    V_FRONT_CLIT_OPT     ;

!LEXICON V_BACK_OPTATIVE_N_ARCHAIC
!+Act+Optat+Sg1+Use/Rare:non   V_BACK_CLIT_OPT     ;
!+Act+Optat+Sg2+Use/Rare:nos    V_BACK_CLIT_OPT     ;

!LEXICON V_FRONT_OPTATIVE_N_ARCHAIC
!+Act+Optat+Sg1+Use/Rare:nön   V_FRONT_CLIT_OPT     ;
!+Act+Optat+Sg2+Use/Rare:nös    V_FRONT_CLIT_OPT     ;



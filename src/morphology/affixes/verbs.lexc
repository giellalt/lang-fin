!! !!!Verb inflection and derivation
!! The verbs' conjugation includes voice (in Finnish grammars also verbal 
!! genus),  tense/mood (tempus/modus), personal endings and negation marker
!! The verbs also have productive derivations to defective nouns as
!! infinitives, and to adjectives as participles, which are considered to be 
!! part of inflection and thus included in most versions 
!! ([VISK § 105|http://scripta.kotus.fi/visk/sisalto.php?p?105]).
!! The morphology of participles and historical 4th infinitive is further
!! detailed in section Deverbal nouns'  morphology.
!!
!! The analysis strings of verb are not as systematic as nouns,
!! as many categories collapse together in forms, e.g. the tense and mood
!! are only distinct with indicative past and non-past, otherwise mood implies
!! tense in semantic sense.

!! !!Present vowel stems

LEXICON V_BACK_WEAK_PRESENT
!!gt-norm: Verb weak present back
!!€ kutoa: kutoa+V+Act+InfA+Sg+Lat # knit
!!€ kudon: kutoa+V+Act+Ind+Prs+Sg1
+Act+Ind+Prs+Sg1:%>n V_BACK_CLIT_OPT     ;
+Act+Ind+Prs+Sg2:%>t V_BACK_CLIT_OPT     ;
+Act+Ind+Prs+Pl1:%>mme   V_BACK_CLIT_OPT     ;
+Act+Ind+Prs+Pl2:%>tte   V_BACK_CLIT_OPT     ;
+Ind+Prs+ConNeg:%>0    V_BACK_CLIT_OPT     ;
+Imprt+Prs+ConNeg:%>0    V_BACK_CLIT_OPT     ;
+Act+Imprt+Sg2:%>0    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_WEAK_PRESENT
!!gt-norm: Verb weak present front
!!€ yltää: yltää+V+Act+InfA+Sg+Lat # reach
!!€ yllän: yltää+V+Act+Ind+Prs+Sg1
+Act+Ind+Prs+Sg1:%>n V_FRONT_CLIT_OPT     ;
+Act+Ind+Prs+Sg2:%>t V_FRONT_CLIT_OPT     ;
+Act+Ind+Prs+Pl1:%>mme   V_FRONT_CLIT_OPT     ;
+Act+Ind+Prs+Pl2:%>tte   V_FRONT_CLIT_OPT     ;
+Ind+Prs+ConNeg:%>0    V_FRONT_CLIT_OPT     ;
+Imprt+Prs+ConNeg:%>0    V_FRONT_CLIT_OPT     ;
+Act+Imprt+Sg2:%>0    V_FRONT_CLIT_OPT     ;

!! The strong form of present indicative endings in strong stems and ma 
!! infinitive, and maisilla derivation (nee infinitive).
LEXICON V_BACK_STRONG_PRESENT
!!€gt-norm: Verb strong present back
!!€ kutovat: kutoa+V+Act+Ind+Prs+Pl3
!!€ kutomatta: kutoa+V+Act+InfMa+Sg+Abe
+Act+Ind+Prs+Pl3:%>vat   V_BACK_CLIT_OPT     ;
+Act+InfMa+Sg+Abe:%>ma%>tta   V_BACK_POSS_AN_OPT      ;
+Act+InfMa+Sg+Ade:%>ma%>lla   V_BACK_CLIT_OPT     ;
+Act+InfMa+Sg+Ela:%>ma%>sta   V_BACK_CLIT_OPT     ;
+Act+InfMa+Sg+Ill:%>ma%>an    V_BACK_CLIT_OPT     ;
+Act+InfMa+Sg+Ine:%>ma%>ssa   V_BACK_CLIT_OPT     ;
+Act+InfMa+Sg+Ins:%>ma%>n    V_BACK_CLIT_OPT     ;
+Act+Der/maisilla:%>ma»isi%>lla   V_BACK_POSS_AN_OBL        ;

LEXICON V_FRONT_STRONG_PRESENT
!!€gt-norm: Verb strong present front
!!€ yltävät: yltää+V+Act+Ind+Prs+Pl3
!!€ yltämättä: yltää+V+Act+InfMa+Sg+Abe
+Act+Ind+Prs+Pl3:%>vät   V_FRONT_CLIT_OPT     ;
+Act+InfMa+Sg+Abe:%>mä%>ttä   V_FRONT_POSS_AN_OPT      ;
+Act+InfMa+Sg+Ade:%>mä%>llä   V_FRONT_CLIT_OPT     ;
+Act+InfMa+Sg+Ela:%>mä%>stä   V_FRONT_CLIT_OPT     ;
+Act+InfMa+Sg+Ill:%>mä%>än    V_FRONT_CLIT_OPT     ;
+Act+InfMa+Sg+Ine:%>mä%>ssä   V_FRONT_CLIT_OPT     ;
+Act+InfMa+Sg+Ins:%>mä%>n    V_FRONT_CLIT_OPT     ;
+Act+Der/maisilla:%>mä»isi%>llä   V_FRONT_POSS_AN_OBL        ;

!! !Verb 3rd singular forms
!! The third singular form of present tense has few allomorphs according to
!! preceding vowel context, either lengthening or zero after long vowel stem:

LEXICON V_BACK_3SG_A
!!€gt-norm: Verb 3rd singular a
!!€ rakentaa: rakentaa+V+Act+Ind+Prs+Sg3 # builds
+Act+Ind+Prs+Sg3:%>a V_BACK_CLIT_OPT     ;

LEXICON V_BACK_3SG_E
!!€gt-norm: Verb 3rd singular e back
!!€ tulee: tulla+V+Act+Ind+Prs+Sg3 # comes
+Act+Ind+Prs+Sg3:%>e V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_3SG_E
!!€gt-norm: Verb 3rd singular e front
!!€ menee: mennä+V+Act+Ind+Prs+Sg3 # goes
+Act+Ind+Prs+Sg3:%>e V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_3SG_I
!!€gt-norm: Verb 3rd singular i back
!!€ munii: munia+V+Act+Ind+Prs+Sg3 # lays eggs
+Act+Ind+Prs+Sg3:%>i V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_3SG_I
!!€gt-norm: Verb 3rd singular e front
!!€ rämpii: rämpiä+V+Act+Ind+Prs+Sg3 # hikes
+Act+Ind+Prs+Sg3:%>i V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_3SG_O
!!€gt-norm: Verb 3rd singular o back
!!€ kutoo: kutoa+V+Act+Ind+Prs+Sg3
+Act+Ind+Prs+Sg3:%>o V_BACK_CLIT_OPT     ;

LEXICON V_BACK_3SG_U
!!€gt-norm: Verb 3rd singular u back
!!€ viruu: virua+V+Act+Ind+Prs+Sg3 # stretches
+Act+Ind+Prs+Sg3:%>u V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_3SG_Y
!!€gt-norm: Verb 3rd singular y
!!€ kääntyy: kääntyä+V+Act+Ind+Prs+Sg3 # turns
+Act+Ind+Prs+Sg3:%>y V_FRONT_CLIT_OPT     ;

LEXICON V_FRONT_3SG_Ö
!!€gt-norm: Verb 3rd singular ö
!!€ säilöö: säilöä+V+Act+Ind+Prs+Sg3 # conserves
+Act+Ind+Prs+Sg3:%>ö V_FRONT_CLIT_OPT     ;

LEXICON V_FRONT_3SG_Ä
!!€gt-norm: Verb 3rd singular ä
!!€ vääntää: vääntää+V+Act+Ind+Prs+Sg3 # twist
+Act+Ind+Prs+Sg3:%>ä V_FRONT_CLIT_OPT     ;

!! !Past forms

LEXICON V_BACK_WEAK_PAST
!!€gt-norm: Verb past back
!!€ kudoin: kutoa+V+Act+Ind+Prt+Sg1
+Act+Ind+Prt+Sg1:%>i%>n   V_BACK_CLIT_OPT     ;
+Act+Ind+Prt+Sg2:%>i%>t   V_BACK_CLIT_OPT     ;
+Act+Ind+Prt+Pl1:%>i%>mme V_BACK_CLIT_OPT     ;
+Act+Ind+Prt+Pl2:%>i%>tte V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_WEAK_PAST
!!€gt-norm: Verb past front
!!€ ylsin: yltää+V+Act+Ind+Prt+Sg1
+Act+Ind+Prt+Sg1:%>i%>n   V_FRONT_CLIT_OPT     ;
+Act+Ind+Prt+Sg2:%>i%>t   V_FRONT_CLIT_OPT     ;
+Act+Ind+Prt+Pl1:%>i%>mme V_FRONT_CLIT_OPT     ;
+Act+Ind+Prt+Pl2:%>i%>tte V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_STRONG_PAST
!!€gt-norm: Verb past back strong
!!€ kutoivat: kutoa+V+Act+Ind+Prt+Pl3
+Act+Ind+Prt+Sg3:%>i    V_BACK_CLIT_OPT     ;
+Act+Ind+Prt+Pl3:%>i%>vat  V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_STRONG_PAST
!!€gt-norm: Verb past front strong
!!€ ylsivät: yltää+V+Act+Ind+Prt+Pl3
+Act+Ind+Prt+Pl3:%>i%>vät  V_FRONT_CLIT_OPT     ;
+Act+Ind+Prt+Sg3:%>i    V_FRONT_CLIT_OPT     ;

!! !Imperatives

LEXICON V_BACK_IMPERATIVE
!!€gt-norm: Verb imperative back
!!€ kutokaa: kutoa+V+Act+Imprt+Pl2
+Act+Imprt+Pl1:%>kaamme   V_BACK_CLIT_OPT     ;
+Act+Imprt+Pl2+Foc/s:%>kaas  V_BACK_CLIT_OPT     ;
+Act+Imprt+Pl2:%>kaa  V_BACK_CLIT_OPT     ;
+Act+Imprt+Pl3:%>koot V_BACK_CLIT_OPT     ;
+Act+Imprt+Sg3:%>koon V_BACK_CLIT_OPT     ;
+Imprt+ConNeg:%>ko   V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_IMPERATIVE
!!€gt-norm: Verb imperative front
!!€ yltäkää: yltää+V+Act+Imprt+Pl2
+Act+Imprt+Pl1:%>käämme   V_FRONT_CLIT_OPT     ;
+Act+Imprt+Pl2+Foc/s:%>kääs  V_FRONT_CLIT_OPT     ;
+Act+Imprt+Pl2:%>kää  V_FRONT_CLIT_OPT     ;
+Act+Imprt+Pl3:%>kööt V_FRONT_CLIT_OPT     ;
+Act+Imprt+Sg3:%>köön V_FRONT_CLIT_OPT     ;
+Imprt+ConNeg:%>kö   V_FRONT_CLIT_OPT     ;

!! !Conditionals

LEXICON V_BACK_CONDITIONAL
!!€gt-norm: Verb conditional back
!!€ kutoisin: kutoa+V+Act+Cond+Sg1
+Act+Cond+Pl1:%>isi%>mme V_BACK_CLIT_OPT     ;
+Act+Cond+Pl2:%>isi%>tte V_BACK_CLIT_OPT     ;
+Act+Cond+Pl3:%>isi%>vat  V_BACK_CLIT_OPT     ;
+Act+Cond+Sg1:%>isi%>n   V_BACK_CLIT_OPT     ;
+Act+Cond+Sg2:%>isi%>t   V_BACK_CLIT_OPT     ;
+Act+Cond+Sg3:%>isi%>    V_BACK_CLIT_OPT     ;
+Cond+ConNeg:%>isi V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_CONDITIONAL
!!€gt-norm: Verb conditional front
!!€ yltäisin: yltää+V+Act+Cond+Sg1
+Act+Cond+Pl1:%>isi%>mme V_FRONT_CLIT_OPT     ;
+Act+Cond+Pl2:%>isi%>tte V_FRONT_CLIT_OPT     ;
+Act+Cond+Pl3:%>isi%>vät  V_FRONT_CLIT_OPT     ;
+Act+Cond+Sg1:%>isi%>n   V_FRONT_CLIT_OPT     ;
+Act+Cond+Sg2:%>isi%>t   V_FRONT_CLIT_OPT     ;
+Act+Cond+Sg3:%>isi%>    V_FRONT_CLIT_OPT     ;
+Cond+ConNeg:%>isi V_FRONT_CLIT_OPT     ;

!! !Potentials

LEXICON V_BACK_POTENTIAL_N
!!€gt-norm: Verb potential n back
!!€ kutonen: kutoa+V+Act+Pot+Sg1
+Act+Pot+Pl1:%>ne%>mme V_BACK_CLIT_OPT     ;
+Act+Pot+Pl2:%>ne%>tte V_BACK_CLIT_OPT     ;
+Act+Pot+Pl3:%>ne%>vat  V_BACK_CLIT_OPT     ;
+Act+Pot+Sg1:%>ne%>n   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg2:%>ne%>t   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg3:%>ne%>e   V_BACK_CLIT_OPT     ;
+Pot+ConNeg:%>ne V_BACK_CLIT_OPT     ;
+Act+PrfPrc:%>n   A_KULUNUT       ;
+Act+Ind+Prt+ConNeg:%>nut  V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_POTENTIAL_N
!!€gt-norm: Verb potential n front
!!€ yltänen: yltää+V+Act+Pot+Sg1
+Act+Pot+Pl1:%>ne%>mme V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl2:%>ne%>tte V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl3:%>ne%>vät  V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg1:%>ne%>n   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg2:%>ne%>t   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg3:%>ne%>e   V_FRONT_CLIT_OPT     ;
+Pot+ConNeg:%>ne V_FRONT_CLIT_OPT     ;
+Act+PrfPrc:%>n   A_ÄLLISTYNYT     ;
+Act+Ind+Prt+ConNeg:%>nyt  V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_POTENTIAL_S
!!€gt-norm: Verb potential s back
!!€ juossen: juosta+V+Act+Pot+Sg1 # I might run
+Act+Pot+Pl1:%>se%>mme V_BACK_CLIT_OPT     ;
+Act+Pot+Pl2:%>se%>tte V_BACK_CLIT_OPT     ;
+Act+Pot+Pl3:%>se%>vat  V_BACK_CLIT_OPT     ;
+Act+Pot+Sg1:%>se%>n   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg2:%>se%>t   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg3:%>se%>e   V_BACK_CLIT_OPT     ;
+Pot+ConNeg:%>se V_BACK_CLIT_OPT     ;
+Act+PrfPrc:%>s   A_KULUNUT       ;
+Act+Ind+Prt+ConNeg:%>sut  V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_POTENTIAL_S
!!€gt-norm: Verb potential s front
!!€ päässen: päästä+V+Act+Pot+Sg1 # I might be able to
+Act+Pot+Pl1:%>se%>mme V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl2:%>se%>tte V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl3:%>se%>vät  V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg1:%>se%>n   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg2:%>se%>t   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg3:%>se%>e   V_FRONT_CLIT_OPT     ;
+Pot+ConNeg:%>se V_FRONT_CLIT_OPT     ;
+Act+PrfPrc:%>s A_ÄLLISTYNYT      ;
+Act+Ind+Prt+ConNeg:%>syt  V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_POTENTIAL_L
!!€gt-norm: Verb potential l back
!!€ tullen: tulla+V+Act+Pot+Sg1
+Act+Pot+Pl1:%>le%>mme V_BACK_CLIT_OPT     ;
+Act+Pot+Pl2:%>le%>tte V_BACK_CLIT_OPT     ;
+Act+Pot+Pl3:%>le%>vat  V_BACK_CLIT_OPT     ;
+Act+Pot+Sg1:%>le%>n   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg2:%>le%>t   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg3:%>le%>e   V_BACK_CLIT_OPT     ;
+Pot+ConNeg:%>le V_BACK_CLIT_OPT     ;
+Act+PrfPrc:%>l  A_KULUNUT      ;
+Act+Ind+Prt+ConNeg:%>lut  V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_POTENTIAL_L
!!€gt-norm: Verb potential l front
!!€ kävellen: kävellä+V+Act+Pot+Sg1 # I might walk
+Act+Pot+Pl1:%>le%>mme V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl2:%>le%>tte V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl3:%>le%>vät  V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg1:%>le%>n   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg2:%>le%>t   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg3:%>le%>e   V_FRONT_CLIT_OPT     ;
+Pot+ConNeg:%>le V_FRONT_CLIT_OPT     ;
+Act+PrfPrc:%>l  A_ÄLLISTYNYT  ;
+Act+Ind+Prt+ConNeg:%>lyt  V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_POTENTIAL_R
!!€gt-norm: Verb potential r back
!!€ surren: surra+V+Act+Pot+Sg1 # I might be sad
+Act+Pot+Pl1:%>re%>mme V_BACK_CLIT_OPT     ;
+Act+Pot+Pl2:%>re%>tte V_BACK_CLIT_OPT     ;
+Act+Pot+Pl3:%>re%>vat  V_BACK_CLIT_OPT     ;
+Act+Pot+Sg1:%>re%>n   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg2:%>re%>t   V_BACK_CLIT_OPT     ;
+Act+Pot+Sg3:%>re%>e   V_BACK_CLIT_OPT     ;
+Pot+ConNeg:%>re V_BACK_CLIT_OPT     ;
+Act+PrfPrc:%>r  A_KULUNUT      ;
+Act+Ind+Prt+ConNeg:%>rut  V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_POTENTIAL_R
!!€gt-norm: Verb potential r front
!!€ pierren: pierrä+V+Act+Pot+Sg1 # I might fart
+Act+Pot+Pl1:%>re%>mme V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl2:%>re%>tte V_FRONT_CLIT_OPT     ;
+Act+Pot+Pl3:%>re%>vät  V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg1:%>re%>n   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg2:%>re%>t   V_FRONT_CLIT_OPT     ;
+Act+Pot+Sg3:%>re%>e   V_FRONT_CLIT_OPT     ;
+Pot+ConNeg:%>re V_FRONT_CLIT_OPT     ;
+Act+PrfPrc:%>r  A_ÄLLISTYNYT  ;
+Act+Ind+Prt+ConNeg:%>ryt  V_FRONT_CLIT_OPT     ;

!! !!Passive forms
!! The passive forms usually contain -ta-, -tä-, -da-, -dä-, element in them.
!! The variation between the realisations is one key factor of determining the
!! classification of the verb roots.

LEXICON V_BACK_PASSIVE
!!€gt-norm: Verb passive back
!!€ kudottiin: kutoa+V+Pss+Ind+Prt+Pe4
!!€ juostiin: juosta+V+Pss+Ind+Prt+Pe4
+Pss+Cond+Pe4:%>ta%>isiin V_BACK_CLIT_OPT     ;
+Pss+Cond+Pe4+ConNeg:%>ta%>isi  V_BACK_CLIT_OPT     ;
+Pss+Imprt+Pe4+ConNeg:%>ta%>ko    V_BACK_CLIT_OPT     ;
+Pss+Imprt+Pe4:%>ta%>koon    V_BACK_CLIT_OPT     ;
+Pss+InfE+Ine:%>ta%>essa  V_BACK_CLIT_OPT      ;
+Pss+InfE+Ins:%>ta%>en   V_BACK_CLIT_OPT     ;
+Pss+InfMa+Sg+Ins:%>ta%>man  V_BACK_CLIT_OPT     ;
+Pss+Pot+Pe4:%>ta%>neen  V_BACK_CLIT_OPT     ;
+Pss+Pot+Pe4+ConNeg:%>ta%>ne   V_BACK_CLIT_OPT     ;
+Pss+Ind+Prt+Pe4:tiin V_BACK_CLIT_OPT     ;
+Pss+PrsPrc:%>ta%>v    N_SOITTAJA   ;

LEXICON V_FRONT_PASSIVE
!!€gt-norm: Verb passive front
!!€ nähtiin: nähdä+V+Pss+Ind+Prt+Pe4
+Pss+Cond+Pe4:%>tä%>isiin V_FRONT_CLIT_OPT     ;
+Pss+Cond+Pe4+ConNeg:%>tä%>isi  V_FRONT_CLIT_OPT     ;
+Pss+Imprt+Pe4+ConNeg:%>tä%>kö    V_FRONT_CLIT_OPT     ;
+Pss+Imprt+Pe4:%>tä%>köön    V_FRONT_CLIT_OPT     ;
+Pss+InfE+Ine:%>tä%>essä  V_FRONT_CLIT_OPT      ;
+Pss+InfE+Ins:%>tä%>en   V_FRONT_CLIT_OPT     ;
+Pss+InfMa+Ins:%>tä%>män  V_FRONT_CLIT_OPT     ;
+Pss+Pot+Pe4:%>tä%>neen  V_FRONT_CLIT_OPT     ;
+Pss+Pot+Pe4+ConNeg:%>tä%>ne   V_FRONT_CLIT_OPT     ;
+Pss+Ind+Prt+Pe4:tiin V_FRONT_CLIT_OPT     ;
+Pss+PrsPrc:%>tä%>v    A_TYÖLLISTETTÄVÄ ;


!! The form of present passive assimilates leftwards, varying between
!! -ta, -tä, -da, -dä, -la, -lä, -ra, -rä, -na, -nä.

LEXICON V_BACK_PASSIVE_T
!!€gt-norm: Verb passive back t
!!€ juostaan: juosta+V+Pss+Ind+Prs+Pe4
+Pss+Ind+Prs+Pe4:%>ta%>an    V_BACK_CLIT_OPT     ;
+Pss+Ind+Prs+Pe4+ConNeg:%>ta    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_PASSIVE_T
!!€gt-norm: Verb passive front t
!!€ ylletään: yltää+V+Pss+Ind+Prs+Pe4
+Pss+Ind+Prs+Pe4:%>tä%>än    V_FRONT_CLIT_OPT     ;
+Pss+Ind+Prs+Pe4+ConNeg:%>tä    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_PASSIVE_D
!!€gt-norm: Verb passive back d
!!€ kopioidaan: kopioida+V+Pss+Ind+Prs+Pe4 # copy-passive
+Pss+Ind+Prs+Pe4:%>da%>an    V_BACK_CLIT_OPT     ;
+Pss+Ind+Prs+Pe4+ConNeg:%>da    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_PASSIVE_D
!!€gt-norm: Verb passive front d
!!€ nähdään: nähdä+V+Pss+Ind+Prs+Pe4
+Pss+Ind+Prs+Pe4:%>dä%>än    V_FRONT_CLIT_OPT     ;
+Pss+Ind+Prs+Pe4+ConNeg:%>dä    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_PASSIVE_L
!!€gt-norm: Verb passive back l
!!€ vuollaan: vuolla+V+Pss+Ind+Prs+Pe4 # carve wood-passive
+Pss+Ind+Prs+Pe4:%>la%>an    V_BACK_CLIT_OPT     ;
+Pss+Ind+Prs+Pe4+ConNeg:%>la    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_PASSIVE_L
!!€gt-norm: Verb passive front l
!!€ niellään: niellä+V+Pss+Ind+Prs+Pe4 # swallow-passive
+Pss+Ind+Prs+Pe4:%>lä%>än    V_FRONT_CLIT_OPT     ;
+Pss+Ind+Prs+Pe4+ConNeg:%>lä    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_PASSIVE_R
!!€gt-norm: Verb passive back r
!!€ surraan: surra+V+Pss+Ind+Prs+Pe4 # sorrow-passive
+Pss+Ind+Prs+Pe4:%>ra%>an    V_FRONT_CLIT_OPT     ;
+Pss+Ind+Prs+Pe4+ConNeg:%>ra    V_FRONT_CLIT_OPT     ;

LEXICON V_FRONT_PASSIVE_R
!!€gt-norm: Verb passive front r
!!€ pierrään: pierrä+V+Pss+Ind+Prs+Pe4 # fart-passive
+Pss+Ind+Prs+Pe4:%>rä%>än    V_FRONT_CLIT_OPT     ;
+Pss+Ind+Prs+Pe4+ConNeg:%>rä    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_PASSIVE_N
!!€gt-norm: Verb passive back n
!!€ pannaan: panna+V+Pss+Ind+Prs+Pe4 # put-passive
+Pss+Ind+Prs+Pe4:naan    V_BACK_CLIT_OPT     ;
+Pss+Ind+Prs+Pe4+ConNeg:na    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_PASSIVE_N
!!€gt-norm: Verb passive front n
!!€ mennään: mennä+V+Pss+Ind+Prs+Pe4
+Pss+Ind+Prs+Pe4:nään    V_FRONT_CLIT_OPT     ;
+Pss+Ind+Prs+Pe4+ConNeg:nä    V_FRONT_CLIT_OPT     ;


!! !!Infinite verb forms

LEXICON V_BACK_AINF
!!€gt-norm: Verb a infinitive
!!€ juoda: juoda+V+Act+InfA+Sg+Lat # drink
+Act+InfA+Sg+Lat:a     V_BACK_CLIT_OPT     ;
+Act+InfA+Sg+Tra:akse  V_BACK_POSS_EN_OBL        ;

LEXICON V_FRONT_AINF
!!€gt-norm: Verb ä infinitive
!!€ nähdä: nähdä+V+Act+InfA+Sg+Lat
+Act+InfA+Sg+Lat:ä     V_FRONT_CLIT_OPT     ;
+Act+InfA+Sg+Tra:äkse  V_FRONT_POSS_EN_OBL        ;

LEXICON V_BACK_EINF
!!€gt-norm: Verb E infinitive back
!!€ kutoessa: kutoa+V+Act+InfE+Sg+Ine
+Act+InfE+Sg+Ine:essa   V_BACK_POSS_AN_OPT      ;
+Act+InfE+Sg+Ins:en    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_EINF
!!€gt-norm: Verb E infinitive front
!!€ yltäessä: yltää+V+Act+InfE+Sg+Ine
+Act+InfE+Sg+Ine:essä   V_FRONT_POSS_AN_OPT      ;
+Act+InfE+Sg+Ins:en    V_FRONT_CLIT_OPT     ;

!! !Participles

LEXICON V_BACK_TUPCP_STRONG
!!€gt-norm: Verb nut participle passive tu
!!€ valkaistu: valkaista+V+Pss+PrfPrc+Sg+Nom # bleached
+Pss+PrfPrc:tu            A_VALKAISTU        ;
+Pss+Ind+Prt+ConNeg:tu    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_TUPCP_STRONG
!!€gt-norm: Verb nut participle passive ty
!!€ häpäisty: häpäistä+V+Pss+PrfPrc+Sg+Nom # sacrileged
+Pss+PrfPrc:ty            A_HÄPÄISTY     ;
+Pss+Ind+Prt+ConNeg:ty    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_TUPCP_0
!!€gt-norm: Verb nut participle passive tu~u
!!€ huudettu: huutaa+V+Pss+PrfPrc+Sg+Nom # yelled
+Pss+PrfPrc:0             A_VIMMATTU      ;
+Pss+Ind+Prt+ConNeg:tu    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_TUPCP_0
!!€gt-norm: Verb nut participle passive ty~y
!!€ ylennetty: ylentää+V+Pss+PrfPrc+Sg+Nom # promoted
+Pss+PrfPrc:0             A_YLENNETTY       ;
+Pss+Ind+Prt+ConNeg:ty    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_TUPCP_D
!!€gt-norm: Verb nut participle passive tu~du
!!€ viipaloitu: viipaloida+V+Pss+PrfPrc+Sg+Nom # sliced
+Pss+PrfPrc:0             A_VIIPALOITU    ;
+Pss+Ind+Prt+ConNeg:tu    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_TUPCP_D
!!€gt-norm: Verb nut participle passive ty~dy
!!€ yksilöity: yksilöidä+V+Pss+PrfPrc+Sg+Nom # singled out
+Pss+PrfPrc:0             A_YKSILÖITY        ;
+Pss+Ind+Prt+ConNeg:ty    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_TUPCP_L
!!€gt-norm: Verb nut participle passive tu~lu
!!€ paranneltu: parannella+V+Pss+PrfPrc+Sg+Nom # embettered
+Pss+PrfPrc:0             A_PARANNELTU  ;
+Pss+Ind+Prt+ConNeg:tu    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_TUPCP_L
!!€gt-norm: Verb nut participle passive ty~ly
!!€ vähätelty: vähätellä+V+Pss+PrfPrc+Sg+Nom # belittled
+Pss+PrfPrc:0             A_VÄHÄTELTY        ;
+Pss+Ind+Prt+ConNeg:ty    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_TUPCP_N
!!€gt-norm: Verb nut participle passive tu~nu
!!€ pantu: panna+V+Pss+PrfPrc+Sg+Nom # put
+Pss+PrfPrc:0             A_PANTU  ;
+Pss+Ind+Prt+ConNeg:tu    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_TUPCP_N
!!€gt-norm: Verb nut participle passive ty~ny
!!€ menty: mennä+V+Pss+PrfPrc+Sg+Nom # went
+Pss+PrfPrc:0             A_MENTY        ;
+Pss+Ind+Prt+ConNeg:ty    V_FRONT_CLIT_OPT     ;

LEXICON V_BACK_TUPCP_R
!!€gt-norm: Verb nut participle passive tu~ru
!!€ purtu: purra+V+Pss+PrfPrc+Sg+Nom # bitten
+Pss+PrfPrc:0             A_PURTU ;
+Pss+Ind+Prt+ConNeg:tu    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_TUPCP_R
!!€gt-norm: Verb nut participle passive ty~ry
!!€ pierty: pierrä+V+Pss+PrfPrc+Sg+Nom
+Pss+PrfPrc:0             A_PIERTY     ;
+Pss+Ind+Prt+ConNeg:ty    V_FRONT_CLIT_OPT     ;

!! !Possessives for infinite verb forms
LEXICON V_FRONT_POSS
!!€gt-norm: Verb possessive front
!!€ kutoakseni: kutoa+V+Act+InfA+Sg+Tra+PxSg1
!!€ kutoessasi: kutoa+V+Act+InfE+Sg+Ine+PxSg2
+PxSg1:ni  V_FRONT_CLIT_OPT        ;
+PxSg2:si  V_FRONT_CLIT_OPT        ;
+PxSg3:nsä V_FRONT_CLIT_OPT        ;
+PxPl1:mme V_FRONT_CLIT_OPT        ;
+PxPl2:nne V_FRONT_CLIT_OPT        ;
+PxPl3:nsä V_FRONT_CLIT_OPT        ;

LEXICON V_BACK_POSS
!!€gt-norm: Verb possessive back
!!€ nähdäkseni: nähdä+V+Act+InfA+Sg+Tra+PxSg1
!!€ nähdessäsi: nähdä+V+Act+InfE+Sg+Ine+PxSg2
+PxSg1:ni  V_BACK_CLIT_OPT        ;
+PxSg2:si  V_BACK_CLIT_OPT        ;
+PxSg3:nsa V_BACK_CLIT_OPT        ;
+PxPl1:mme V_BACK_CLIT_OPT        ;
+PxPl2:nne V_BACK_CLIT_OPT        ;
+PxPl3:nsa V_BACK_CLIT_OPT        ;

LEXICON V_BACK_POSS_AN
!!€gt-norm: Verb possessive back an
!!€ kutomattaan: kutoa+V+Act+InfMa+Sg+Abe+PxSg3
+PxSg3:an    V_BACK_CLIT_OPT     ;
+PxPl3:an    V_BACK_CLIT_OPT     ;

LEXICON V_BACK_POSS_EN
!!€gt-norm: Verb possessive back en
!!€ kutoakseen: kutoa+V+Act+InfA+Sg+Tra+PxPl3
+PxSg3:en    V_BACK_CLIT_OPT     ;
+PxPl3:en    V_BACK_CLIT_OPT     ;

LEXICON V_FRONT_POSS_EN
!!€gt-norm: Verb possessive front en
!!€ nähdäkseen: nähdä+V+Act+InfA+Sg+Tra+PxPl3
+PxSg3:en    V_FRONT_CLIT_OPT     ;
+PxPl3:en    V_FRONT_CLIT_OPT     ;

LEXICON V_FRONT_POSS_AN
!!€gt-norm: Verb possessive back än
!!€ näkemättään: nähdä+V+Act+InfMa+Sg+Abe+PxSg3
+PxSg3:än    V_FRONT_CLIT_OPT     ;
+PxPl3:än    V_FRONT_CLIT_OPT     ;

!! !Verb clitics

LEXICON V_BACK_CLIT
!!€gt-norm: Verbs clitic back
!!€ kudonhan: kutoa+V+Act+Ind+Prs+Sg1+Foc/han
!!€ kudotko: kutoa+V+Act+Ind+Prs+Sg2+Qst
+Foc/han:han   #        ;
+Foc/han+Foc/kaan:hankaan  #        ;
+Foc/han+Qst:hanko #        ;
+Foc/han+Foc/pa:hanpa #        ;
+Qst:ko #        ;
+Qst+Foc/han:kohan #        ;
+Qst+Foc/kaan:kokaan    #        ;
+Qst+Foc/kin:kokin    #        ;
+Qst+Foc/pa:kopa   #        ;
+Qst+Foc/s:kos    #        ;
+Foc/pa:pa #        ;
+Foc/pa+Foc/han:pahan #        ;
+Foc/pa+Foc/kaan:pakaan    #        ;
+Foc/pa+Foc/kin:pakin    #        ;
+Foc/pa+Qst:pako   #        ;
+Foc/pa+Foc/s:pas    #        ;
+Foc/kin:kin  #        ;
+Foc/kin+Foc/han:kinhan  #        ;
+Foc/kin+Foc/kaan:kinkaan #        ;
+Foc/kin+Qst:kinko    #        ;
+Foc/kin+Foc/pa:kinpa    #        ;
+Foc/kaan:kaan  #        ;
+Foc/kaan+Foc/han:kaanhan  #        ;
+Foc/kaan+Foc/kin:kaankin #        ;
+Foc/kaan+Qst:kaanko    #        ;
+Foc/kaan+Foc/pa:kaanpa    #        ;

LEXICON V_FRONT_CLIT
!!€gt-norm: Verbs clitic front
!!€ näenhän: nähdä+V+Act+Ind+Prs+Sg1+Foc/han
!!€ näetkö: nähdä+V+Act+Ind+Prs+Sg2+Qst
+Foc/han:hän   #        ;
+Foc/han+Foc/kaan:hänkään  #        ;
+Foc/han+Qst:hänkö #        ;
+Foc/han+Foc/pa:hänpä #        ;
+Qst:kö #        ;
+Qst+Foc/han:köhän #        ;
+Qst+Foc/kaan:kökään    #        ;
+Qst+Foc/kin:kökin    #        ;
+Qst+Foc/pa:köpä   #        ;
+Qst+Foc/s:kös    #        ;
+Foc/pa:pä #        ;
+Foc/pa+Foc/han:pähän #        ;
+Foc/pa+Foc/kaan:päkään    #        ;
+Foc/pa+Foc/kin:päkin    #        ;
+Foc/pa+Qst:päkö   #        ;
+Foc/pa+Foc/s:päs    #        ;
+Foc/kin:kin  #        ;
+Foc/kin+Foc/han:kinhän  #        ;
+Foc/kin+Foc/kaan:kinkään #        ;
+Foc/kin+Qst:kinkö    #        ;
+Foc/kin+Foc/pa:kinpä    #        ;
+Foc/kaan:kään  #        ;
+Foc/kaan+Foc/han:käänhän  #        ;
+Foc/kaan+Foc/kin:käänkin #        ;
+Foc/kaan+Qst:käänkö    #        ;
+Foc/kaan+Foc/pa:käänpä    #        ;

! Shorthands are not tested or documented yet
LEXICON V_BACK_CLIT_OPT
0   V_BACK_CLIT    ;
0   #   ;

LEXICON V_FRONT_CLIT_OPT
0   V_FRONT_CLIT    ;
0   #   ;

LEXICON V_BACK_POSS_EN_OBL
0   V_BACK_POSS  ;
0   V_BACK_POSS_EN ;

LEXICON V_FRONT_POSS_EN_OBL
0   V_FRONT_POSS  ;
0   V_FRONT_POSS_EN ;

LEXICON V_BACK_POSS_AN_OBL
0   V_BACK_POSS  ;
0   V_BACK_POSS_AN ;

LEXICON V_BACK_POSS_OPT
0   V_BACK_POSS  ;
0   #   ;

LEXICON V_BACK_POSS_AN_OPT
0   V_BACK_POSS  ;
0   V_BACK_POSS_AN ;
0   #   ;

LEXICON V_FRONT_POSS_AN_OBL
0   V_FRONT_POSS  ;
0   V_FRONT_POSS_AN ;

!LEXICON V_FRONT_POSS_OPT
!0   V_FRONT_POSS  ;
!0   #   ;

LEXICON V_FRONT_POSS_AN_OPT
0   V_FRONT_POSS  ;
0   V_FRONT_POSS_AN ;
0   #   ;

!! !!Deverbal derivations
!! Part of the deverbal derivation system in Finnish is so regular that it has
!! been included as part of inflectional morphology in many traditional systems.
!! These derivations are treated as inflection in our system as well.

!! !-minen, "Fourth infinitive"

!LEXICON V_BACK_DERIV_MINEN
!€gt-norm: Verbs minen derivations
!€ aakkostaminen aakkostaa+V+Der/minen+Sg+Nom
!+Der/minen:mi  N_AAKKOSTAMINEN   ;

!LEXICON V_FRONT_DERIV_MINEN
!€ näkeminen nähdä+V+Der/minen+Sg+Nom
!+Der/minen:mi  N_KYLKIÄINEN   ;

!! !Participles

LEXICON V_BACK_DERIV_PCPS
!!€gt-norm: Verbs participle back
!!€ kutomani: kutoa+V+AgPrc+Sg+Nom+PxSg1
!!€ kutomaton: kutoa+V+NegPrc+Sg+Nom
!!€ kutova: kutoa+V+Act+PrsPrc+Sg+Nom
+AgPrc:m       A_AALTOILEVA        ;
+NegPrc:mat     A_VIATON      ;
+Act+PrsPrc:v       A_AALTOILEVA      ;
+Der/minen:mi      N_AAKKOSTAMINEN        ;
+Act+PrsPrc+Der/inen:vai     N_AAKKOSTAMINEN  ;

LEXICON V_FRONT_DERIV_PCPS
!!€gt-norm: Verbs participle front
!!€ näkemäni: nähdä+V+AgPrc+Sg+Nom+PxSg1
!!€ näkemätön: nähdä+V+NegPrc+Sg+Nom
!!€ näkevä: nähdä+V+Act+PrsPrc+Sg+Nom
+AgPrc:m       A_TYÖLLISTETTÄVÄ        ;
+NegPrc:mät     A_KYVYTÖN       ;
+Act+PrsPrc:v       A_TYÖLLISTETTÄVÄ       ;
+Der/minen:mi      N_KYLKIÄINEN        ;
+Act+PrsPrc+Der/inen:väi     N_KYLKIÄINEN  ;

!! !Other derivations
!LEXICON N_BACK_DERIV_NTI
!+Der/nti:n N_SOINTI ;
!LEXICON N_FRONT_DERIV_NTI
!+Der/nti:n N_VIENTI ;

!! Not used in inflectional morphology, yet.

!LEXICON N_BACK_DERIV_VAINEN
!+PrsPrc+Der/inen:vai  N_AAKKOSTAMINEN ;

!LEXICON N_FRONT_DERIV_VÄINEN
!+PrsPrc+Der/inen:väi    N_KYLKIÄINEN   ;

!LEXICON N_BACK_DERIV_JA
!+Der/ja:j     N_MAKKARA    ;

!LEXICON N_FRONT_DERIV_JÄ
!+Der/ja:j     N_HÄKKYRÄ ;

!LEXICON N_BACK_DERIV_VUUS
!+PrsPrc+Drv/uus:vuu     N_AAKKOSELLISUUS  ;

!LEXICON N_FRONT_DERIV_VUUS
!+PrsPrc+Drv/uus:vyy   N_KÖYHYYS ;

!LEXICON V_BACK_DERIV_CAUSATIVE
!+Der/ttaa:t       V_VIEROITTAA ;
!+Der/tattaa:tat     V_VIEROITTAA ;
!+Der/tatuttaa:tatut   V_VIEROITTAA ;

!LEXICON V_FRONT_DERIV_CAUSATIVE
!+Der/ttaa:t       V_RÄPSYTTÄÄ ;
!+Der/tattaa:tat     V_RÄPSYTTÄÄ ;
!+Der/tatuttaa:tatut   V_RÄPSYTTÄÄ ;

!LEXICON V_BACK_DERIV_TUUS
!0:tuu     N_AAKKOSELLISUUS        ;

!LEXICON V_FRONT_DERIV_TYYS
!0:tyy   N_KÖYHYYS   ;

! Spoken
!LEXICON V_BACK_CONDITIONAL_DIAL
!+Act+Cond+Sg1+Qst+Dial/Standard:inks V_BACK_CLIT_OPT ;
!+Act+Cond+Sg2+Qst+Dial/Standard:iks V_BACK_CLIT_OPT ;
!+Act+Cond+Sg3+Dial/Standard:is  V_BACK_CLIT_OPT     ;

!LEXICON V_BACK_PASSIVE_DIAL
!+Pss+Cond+Pe4+ConNeg+Dial/Standard:tais    V_BACK_CLIT_OPT     ;
!+Pss+Ind+Prt+Pe4+Dial/Standard:tii   V_BACK_CLIT_OPT     ;

! Archaisms
!LEXICON V_BACK_IMPERATIVE_ARCHAIC
! The first singular imperative, the long forms of the second plural imperatives
! and the optatives are rare, poetic, archaic, etc:
!+Act+Imprt+Sg1+Use/Rare:kaani    V_BACK_CLIT_OPT     ;
!+Act+Imprt+Sg1+Use/Rare:kaami    V_BACK_CLIT_OPT     ;
!+Act+Imprt+Pl2+Use/Rare:kaatte   V_BACK_CLIT_OPT     ;
!+Act+Imprt+Pl2+Use/Rare:kaatten  V_BACK_CLIT_OPT     ;
!+Act+Optat+Sg2+Use/Rare:kootte  V_BACK_CLIT_OPT     ;
!+Act+Optat+Pl1+Use/Rare:koomme  V_BACK_CLIT_OPT     ;
!+Act+Optat+Pl2+Use/Rare:kaa  V_BACK_CLIT_OPT     ;

!LEXICON V_FRONT_IMPERATIVE_ARCHAIC
! The first singular imperative, the long forms of the second plural imperatives
! and the optatives are rare, poetic, archaic, etc:
!+Act+Imprt+Sg1+Use/Rare:kaani    V_FRONT_CLIT_OPT     ;
!+Act+Imprt+Sg1+Use/Rare:käämi    V_FRONT_CLIT_OPT     ;
!+Act+Imprt+Pl2+Use/Rare:käätte   V_FRONT_CLIT_OPT     ;
!+Act+Imprt+Pl2+Use/Rare:käätten  V_FRONT_CLIT_OPT     ;
!+Act+Optat+Sg2+Use/Rare:köötte  V_FRONT_CLIT_OPT     ;
!+Act+Optat+Pl1+Use/Rare:köömme  V_FRONT_CLIT_OPT     ;
!+Act+Optat+Pl2+Use/Rare:kää  V_FRONT_CLIT_OPT     ;

!LEXICON V_BACK_OPTATIVE_0_ARCHAIC
!+Act+Optat+Sg1+Use/Rare:on   V_BACK_CLIT_OPT     ;
!+Act+Optat+Sg2+Use/Rare:os    V_BACK_CLIT_OPT     ;

!LEXICON V_FRONT_OPTATIVE_0_ARCHAIC
!+Act+Optat+Sg1+Use/Rare:ön   V_FRONT_CLIT_OPT     ;
!+Act+Optat+Sg2+Use/Rare:ös    V_FRONT_CLIT_OPT     ;

!LEXICON V_BACK_OPTATIVE_N_ARCHAIC
!+Act+Optat+Sg1+Use/Rare:non   V_BACK_CLIT_OPT     ;
!+Act+Optat+Sg2+Use/Rare:nos    V_BACK_CLIT_OPT     ;

!LEXICON V_FRONT_OPTATIVE_N_ARCHAIC
!+Act+Optat+Sg1+Use/Rare:nön   V_FRONT_CLIT_OPT     ;
!+Act+Optat+Sg2+Use/Rare:nös    V_FRONT_CLIT_OPT     ;



! Nominal inflection and derivation

! nominatives
LEXICON N_BACK_NOMINATIVE
!!€gt-norm: nominatives
!!€ talo talo+N+Sg+Nom a house
!!€ taloko talo+N+Sg+Nom+Qst
!!$ talopä talo+N+Sg+Nom+Foc/pa
!!¥ talo+N+Sg+Nom+Foc/han: [talohan, ~talohän]
+Sg+Nom:0   N_BACK_CLIT_OPT  ;

LEXICON N_FRONT_NOMINATIVE
!!€ pöly pöly+N+Sg+Nom dust
!!$ pölyko pöly+N+Sg+Nom+Qst
+Sg+Nom:0   N_FRONT_CLIT_OPT  ;

LEXICON N_BACK_PLNOM_CLIT
!!€not-finished: The test below this line are not done yet
!!€ sakset sakset+N+Pl+Nom scissors
!!¥ sakset+N+Pl+Nom+Foc/pa: [saksetpa, ~saksetpä]
+Pl+Nom:%>t     N_BACK_CLIT_OPT  ;

LEXICON N_FRONT_PLNOM_CLIT
!!€ häät häät+N+Pl+Non # wedding
+Pl+Nom:%>t     N_FRONT_CLIT_OPT  ;

! basic cases
LEXICON N_BACK_WEAK_SINGULARS
!!€ padassa
0    N_BACK_PLNOM_CLIT   ;
+Sg+Gen:%>n     N_BACK_CLIT_OPT   ;
+Sg+Abe:%>tta   N_BACK_POSS_AN_OPT      ;
+Sg+Abl:%>lta   N_BACK_POSS_AN_OPT      ;
+Sg+Ade:%>lla   N_BACK_POSS_AN_OPT      ;
+Sg+All:%>lle   N_BACK_POSS_EN_OPT      ;
+Sg+Ela:%>sta   N_BACK_POSS_AN_OPT      ;
+Sg+Ine:%>ssa   N_BACK_POSS_AN_OPT      ;
+Sg+Tra:%>kse   N_BACK_POSS_EN_OBL       ;
+Sg+Tra:%>ksi   N_BACK_CLIT_OPT     ;
+Sg+Tra:%>ksi   N_BACK_CLIT_OPT     ;

LEXICON N_FRONT_WEAK_SINGULARS
!!€ näyssä
0    N_FRONT_PLNOM_CLIT   ;
+Sg+Gen:%>n     N_FRONT_CLIT_OPT   ;
+Sg+Abe:%>ttä   N_FRONT_POSS_ÄN_OPT      ;
+Sg+Abl:%>ltä   N_FRONT_POSS_ÄN_OPT      ;
+Sg+Ade:%>llä   N_FRONT_POSS_ÄN_OPT      ;
+Sg+All:%>lle   N_FRONT_POSS_EN_OPT      ;
+Sg+Ela:%>stä   N_FRONT_POSS_ÄN_OPT      ;
+Sg+Ine:%>ssä   N_FRONT_POSS_ÄN_OPT      ;
+Sg+Tra:%>kse   N_FRONT_POSS_EN_OBL       ;
+Sg+Tra:%>ksi   N_FRONT_CLIT_OPT     ;
+Sg+Tra:%>ksi   N_FRONT_CLIT_OPT     ;

LEXICON N_BACK_STRONG_SINGULARS
!!€ patana
+Sg+Ess:%>na N_BACK_POSS_AN_OPT      ;
+Sg+Nom:0  N_BACK_POSS   ;
+Sg+Gen:0  N_BACK_POSS   ;
+Pl+Nom:0  N_BACK_POSS   ;

LEXICON N_FRONT_STRONG_SINGULARS
!!€ näkynä
+Sg+Ess:%>nä N_FRONT_POSS_ÄN_OPT      ;
+Sg+Nom:0  N_FRONT_POSS   ;
+Sg+Gen:0  N_FRONT_POSS   ;
+Pl+Nom:0  N_FRONT_POSS   ;

LEXICON N_BACK_WEAK_PLURALS
!!€ padoitta
+Pl+Abe:%>i%>tta   N_BACK_POSS_AN_OPT      ;
+Pl+Abl:%>i%>lta   N_BACK_POSS_AN_OPT      ;
+Pl+Ade:%>i%>lla   N_BACK_POSS_AN_OPT      ;
+Pl+All:%>i%>lle  N_BACK_POSS_AN_OPT      ;
+Pl+Ela:%>i%>sta   N_BACK_POSS_AN_OPT      ;
+Pl+Ine:%>i%>ssa   N_BACK_POSS_AN_OPT      ;
+Pl+Ins:%>i%>n    N_BACK_CLIT_OPT     ;
+Pl+Tra:%>i%>kse  N_BACK_POSS_EN_OBL        ;
+Pl+Tra:%>i%>ksi  N_BACK_CLIT_OPT     ;

LEXICON N_FRONT_WEAK_PLURALS
!!€ näyittä
+Pl+Abe:%>i%>ttä   N_FRONT_POSS_ÄN_OPT      ;
+Pl+Abl:%>i%>ltä   N_FRONT_POSS_ÄN_OPT      ;
+Pl+Ade:%>i%>llä   N_FRONT_POSS_ÄN_OPT      ;
+Pl+All:%>i%>lle  N_FRONT_POSS_ÄN_OPT      ;
+Pl+Ela:%>i%>stä   N_FRONT_POSS_ÄN_OPT      ;
+Pl+Ine:%>i%>ssä   N_FRONT_POSS_ÄN_OPT      ;
+Pl+Ins:%>i%>n    N_FRONT_CLIT_OPT     ;
+Pl+Tra:%>i%>kse  N_FRONT_POSS_EN_OBL        ;
+Pl+Tra:%>i%>ksi  N_FRONT_CLIT_OPT     ;

LEXICON N_FRONT_STRONG_PLURALS
!!€ näkyinä
+Pl+Ess:%>i%>nä   N_FRONT_POSS_ÄN_OPT      ;
+Cmt:%>i%>ne        N_FRONT_POSS_EN_OPT      ;

LEXICON N_BACK_STRONG_PLURALS
!!€ patoina
+Pl+Ess:%>i%>na   N_BACK_POSS_AN_OPT      ;
+Cmt:%>i%>ne        N_BACK_POSS_EN_OPT      ;


! allomorphs
LEXICON N_BACK_PARTITIVE_A
!!€ politiikkaa
!! $ politiikkaaan
+Sg+Par:%>a      N_BACK_POSS_OPT      ;

LEXICON N_FRONT_PARTITIVE_Ä
!!€ kysyntää
!! $ kysyntäään
+Sg+Par:%>ä      N_FRONT_POSS_OPT      ;

LEXICON N_BACK_PARTITIVE_A_AN
!!€ patoa
!!€ patoaan
+Sg+Par:%>a      N_BACK_POSS_AN_OPT      ;

LEXICON N_FRONT_PARTITIVE_Ä_ÄN
!!€ näkyä
!!€ näkyään
0+Sg+Par:%>ä     N_FRONT_POSS_ÄN_OPT     ;

LEXICON N_BACK_PARTITIVE_TA
!!€ taimenta
+Sg+Par:%>ta     N_BACK_POSS_AN_OPT      ;

LEXICON N_FRONT_PARTITIVE_TÄ
!!€ tietä
+Sg+Par:%>tä     N_FRONT_POSS_ÄN_OPT      ;

LEXICON N_BACK_ILLATIVE_HAN
!!€ maahan
+Sg+Ill:%>han    #         ;
+Sg+Ill:%>ha     N_BACK_POSS     ;

LEXICON N_BACK_ILLATIVE_HEN
!!€ toffeehen
+Sg+Ill:%>hen    #         ;
+Sg+Ill:%>he     N_BACK_POSS     ;

LEXICON N_FRONT_ILLATIVE_HEN
!!€ tiehen
+Sg+Ill:%>hen    #         ;
+Sg+Ill:%>he     N_FRONT_POSS     ;

LEXICON N_BACK_ILLATIVE_HIN
!!€ haihin
+Sg+Ill:%>hin    #         ;
+Sg+Ill:%>hi     N_BACK_POSS     ;

LEXICON N_FRONT_ILLATIVE_HIN
!!€ piihin
+Sg+Ill:%>hin    #         ;
+Sg+Ill:%>hi     N_FRONT_POSS     ;

LEXICON N_BACK_ILLATIVE_HON
!!€ suohon
+Sg+Ill:%>hon    #         ;
+Sg+Ill:%>ho     N_BACK_POSS     ;

LEXICON N_BACK_ILLATIVE_HUN
!!€ puuhun
+Sg+Ill:%>hun    #         ;
+Sg+Ill:%>hu     N_BACK_POSS     ;

LEXICON N_FRONT_ILLATIVE_HYN
!!€ pyyhyn
+Sg+Ill:%>hyn    #         ;
+Sg+Ill:%>hy     N_FRONT_POSS     ;

LEXICON N_FRONT_ILLATIVE_HÄN
!!€ päähän
+Sg+Ill:%>hän    #         ;
+Sg+Ill:%>hä     N_FRONT_POSS     ;

LEXICON N_FRONT_ILLATIVE_HÖN
!!€ työhön
+Sg+Ill:%>hön    #         ;
+Sg+Ill:%>hö     N_FRONT_POSS     ;

LEXICON N_BACK_PARTITIVE_IA
!!€ koiria
+Pl+Par:%>i%>a    N_BACK_POSS_AN_OPT      ;

LEXICON N_FRONT_PARTITIVE_IÄ
!!€ kärsiä
+Pl+Par:%>i%>ä    N_FRONT_POSS_ÄN_OPT      ;

LEXICON N_BACK_GENITIVE_IDEN
!!€ lepakoiden
+Pl+Gen:%>i%>den    #     ;
+Pl+Gen:%>i%>de     N_BACK_POSS     ;

LEXICON N_FRONT_GENITIVE_IDEN
!!€ kännyköiden
+Pl+Gen:%>i%>den    #     ;
+Pl+Gen:%>i%>de     N_FRONT_POSS     ;

LEXICON N_BACK_GENITIVE_IEN
!!€ naisien
+Pl+Gen:%>i%>en   #     ;
+Pl+Gen:%>i%>e    N_BACK_POSS     ;

LEXICON N_FRONT_GENITIVE_IEN
!!€ murhien
+Pl+Gen:%>i%>en   #     ;
+Pl+Gen:%>i%>e    N_FRONT_POSS     ;

LEXICON N_BACK_ILLATIVE_IHIN
!!€ taloihin
+Pl+Ill:%>i%>hin   #    ;
+Pl+Ill:%>i%>hi    N_BACK_POSS     ;

LEXICON N_FRONT_ILLATIVE_IHIN
!!€ näkyihin
+Pl+Ill:%>i%>hin   #    ;
+Pl+Ill:%>i%>hi    N_FRONT_POSS     ;

LEXICON N_BACK_ILLATIVE_IIN
!!€ koiriin
+Pl+Ill:%>i%>in    #     ;
+Pl+Ill:%>i%>i     N_BACK_POSS     ;

LEXICON N_FRONT_ILLATIVE_IIN
!!€ sieniin
+Pl+Ill:%>i%>in    #     ;
+Pl+Ill:%>i%>i     N_FRONT_POSS     ;

LEXICON N_BACK_GENITIVE_IN
!!€ patain
+Pl+Gen:%>i%>n    #     ;
+Pl+Gen:%>i     N_BACK_POSS     ;

LEXICON N_FRONT_GENITIVE_IN
!!€ tyrmäin
+Pl+Gen:%>i%>n    #     ;
+Pl+Gen:%>i     N_FRONT_POSS     ;

LEXICON N_BACK_ILLATIVE_ISIIN
!!€ sokeisiin
+Pl+Ill:%>i%>siin  #     ;
+Pl+Ill:%>i%>sii   N_BACK_POSS     ;

LEXICON N_FRONT_ILLATIVE_ISIIN
!!€ keväisiin
+Pl+Ill:%>i%>siin  #     ;
+Pl+Ill:%>i%>sii   N_FRONT_POSS     ;

LEXICON N_BACK_PARTITIVE_ITA
!!€ tienoita
+Pl+Par:%>i%>ta   N_BACK_POSS_AN_OPT      ;

LEXICON N_FRONT_PARTITIVE_ITÄ
!!€ teitä
+Pl+Par:%>i%>tä   N_FRONT_POSS_ÄN_OPT      ;

LEXICON N_BACK_GENITIVE_ITTEN
!!€ tienoitten
+Pl+Gen:%>i%>tten     #     ;
+Pl+Gen:%>i%>tte      N_BACK_POSS     ;

LEXICON N_FRONT_GENITIVE_ITTEN
!!€ teitten
+Pl+Gen:%>i%>tten     #     ;
+Pl+Gen:%>i%>tte      N_FRONT_POSS     ;

LEXICON N_BACK_PARTITIVE_JA
!!€ lepakkoja
+Pl+Par:%>j%>a     N_BACK_POSS_AN_OPT      ;

LEXICON N_FRONT_PARTITIVE_JÄ
!!€ pyhäkköjä
+Pl+Par:%>j%>ä     N_FRONT_POSS_ÄN_OPT      ;

LEXICON N_BACK_GENITIVE_JEN
!!€ lepakkojen
+Pl+Gen:%>j%>en    #     ;
+Pl+Gen:%>j%>e     N_BACK_POSS     ;

LEXICON N_FRONT_GENITIVE_JEN
!!€ pyhäkköjen
+Pl+Gen:%>j%>en    #     ;
+Pl+Gen:%>j%>e     N_FRONT_POSS     ;

LEXICON N_BACK_ILLATIVE_SEEN
!!€ tienooseen
+Sg+Ill:%>seen      #     ;
+Sg+Ill:%>see       N_BACK_POSS     ;

LEXICON N_FRONT_ILLATIVE_SEEN
!!€ miljööseen
+Sg+Ill:%>seen      #     ;
+Sg+Ill:%>see       N_FRONT_POSS     ;

LEXICON N_BACK_GENITIVE_TEN
!!€ sisarten
+Pl+Gen:%>ten    #     ;
+Pl+Gen:%>te     N_BACK_POSS     ;

LEXICON N_FRONT_GENITIVE_TEN
!!€ väänninten
+Pl+Gen:%>ten    #     ;
+Pl+Gen:%>te     N_FRONT_POSS     ;

LEXICON N_BACK_ILLATIVE_AN
!!€ kirjaan
+Sg+Ill:%>an     #         ;
+Sg+Ill:%>a      N_BACK_POSS     ;

LEXICON N_BACK_ILLATIVE_EN
!!€ tuleen
+Sg+Ill:%>en     #         ;
+Sg+Ill:%>e      N_BACK_POSS     ;

LEXICON N_FRONT_ILLATIVE_EN
!!€ hiireen
+Sg+Ill:%>en     #         ;
+Sg+Ill:%>e      N_FRONT_POSS     ;

LEXICON N_BACK_ILLATIVE_IN
!!€ ruuviin
+Sg+Ill:%>in     #         ;
+Sg+Ill:%>i      N_BACK_POSS     ;

LEXICON N_FRONT_ILLATIVE_IN
!!€ tyyliin
+Sg+Ill:%>in     #         ;
+Sg+Ill:%>i      N_FRONT_POSS     ;

LEXICON N_BACK_ILLATIVE_ON
!!€ taloon
+Sg+Ill:%>on     #         ;
+Sg+Ill:%>o      N_BACK_POSS     ;

LEXICON N_BACK_ILLATIVE_UN
!!€ nieluun
+Sg+Ill:%>un     #         ;
+Sg+Ill:%>u      N_BACK_POSS     ;

LEXICON N_FRONT_ILLATIVE_YN
!!€ näkyyn
+Sg+Ill:%>yn     #         ;
+Sg+Ill:%>y      N_FRONT_POSS     ;

LEXICON N_FRONT_ILLATIVE_ÄN
!!€ räkään
+Sg+Ill:%>än     #         ;
+Sg+Ill:%>ä      N_FRONT_POSS     ;

LEXICON N_FRONT_ILLATIVE_ÖN
!!€ tönöön
+Sg+Ill:%>ön     #         ;
+Sg+Ill:%>ö      N_FRONT_POSS     ;

! possessives
LEXICON N_FRONT_POSS
!!€ taloni
+PxSg1:%>ni  N_FRONT_CLIT_OPT        ;
+PxSg2:%>si  N_FRONT_CLIT_OPT        ;
+PxSg3:%>nsä N_FRONT_CLIT_OPT        ;
+PxPl1:%>mme N_FRONT_CLIT_OPT        ;
+PxPl2:%>nne N_FRONT_CLIT_OPT        ;
+PxPl3:%>nsä N_FRONT_CLIT_OPT        ;

LEXICON N_BACK_POSS
!!€ tönönsä
+PxSg1:%>ni  N_BACK_CLIT_OPT        ;
+PxSg2:%>si  N_BACK_CLIT_OPT        ;
+PxSg3:%>nsa N_BACK_CLIT_OPT        ;
+PxPl1:%>mme N_BACK_CLIT_OPT        ;
+PxPl2:%>nne N_BACK_CLIT_OPT        ;
+PxPl3:%>nsa N_BACK_CLIT_OPT        ;

LEXICON N_BACK_POSS_AN
!!€ taloaan
+PxSg3:%>an    N_BACK_CLIT_OPT     ;
+PxPl3:%>an    N_BACK_CLIT_OPT     ;

LEXICON N_BACK_POSS_EN
!!€ tienookseen
+PxSg3:%>en    N_BACK_CLIT_OPT     ;
+PxPl3:%>en    N_BACK_CLIT_OPT     ;

LEXICON N_FRONT_POSS_EN
!!€ tiekseen
+PxSg3:%>en    N_FRONT_CLIT_OPT     ;
+PxPl3:%>en    N_FRONT_CLIT_OPT     ;

LEXICON N_FRONT_POSS_ÄN
!!€ tönöään
+PxSg3:%>än    N_FRONT_CLIT_OPT     ;
+PxPl3:%>än    N_FRONT_CLIT_OPT     ;

LEXICON N_BACK_CLIT
!!€ talohan
+Foc/han:%>han   #        ;
+Foc/han+Foc/kaan:%>hankaan  #        ;
+Foc/han+Qst:%>hanko #        ;
+Foc/han+Foc/pa:%>hanpa #        ;
+Qst:%>ko #        ;
+Qst+Foc/han:%>kohan #        ;
+Qst+Foc/kaan:%>kokaan    #        ;
+Qst+Foc/kin:%>kokin    #        ;
+Qst+Foc/pa:%>kopa   #        ;
+Qst+Foc/s:%>kos    #        ;
+Foc/pa:%>pa #        ;
+Foc/pa+Foc/han:%>pahan #        ;
+Foc/pa+Foc/kaan:%>pakaan    #        ;
+Foc/pa+Foc/kin:%>pakin    #        ;
+Foc/pa+Qst:%>pako   #        ;
+Foc/pa+Foc/s:%>pas    #        ;
+Foc/kin:%>kin  #        ;
+Foc/kin+Foc/han:%>kinhan  #        ;
+Foc/kin+Foc/kaan:%>kinkaan #        ;
+Foc/kin+Qst:%>kinko    #        ;
+Foc/kin+Foc/pa:%>kinpa    #        ;
+Foc/kaan:%>kaan  #        ;
+Foc/kaan+Foc/han:%>kaanhan  #        ;
+Foc/kaan+Foc/kin:%>kaankin #        ;
+Foc/kaan+Qst:%>kaanko    #        ;
+Foc/kaan+Foc/pa:%>kaanpa    #        ;

LEXICON N_FRONT_CLIT
!!€ tönöhän
+Foc/han:%>hän   #        ;
+Foc/han+Foc/kaan:%>hänkään  #        ;
+Foc/han+Qst:%>hänkö #        ;
+Foc/han+Foc/pa:%>hänpä #        ;
+Qst:%>kö #        ;
+Qst+Foc/han:%>köhän #        ;
+Qst+Foc/kaan:%>kökään    #        ;
+Qst+Foc/kin:%>kökin    #        ;
+Qst+Foc/pa:%>köpä   #        ;
+Qst+Foc/s:%>kös    #        ;
+Foc/pa:%>pä #        ;
+Foc/pa+Foc/han:%>pähän #        ;
+Foc/pa+Foc/kaan:%>päkään    #        ;
+Foc/pa+Foc/kin:%>päkin    #        ;
+Foc/pa+Qst:%>päkö   #        ;
+Foc/pa+Foc/s:%>päs    #        ;
+Foc/kin:%>kin  #        ;
+Foc/kin+Foc/han:%>kinhän  #        ;
+Foc/kin+Foc/kaan:%>kinkään #        ;
+Foc/kin+Qst:%>kinkö    #        ;
+Foc/kin+Foc/pa:%>kinpä    #        ;
+Foc/kaan:%>kään  #        ;
+Foc/kaan+Foc/han:%>käänhän  #        ;
+Foc/kaan+Foc/kin:%>käänkin #        ;
+Foc/kaan+Qst:%>käänkö    #        ;
+Foc/kaan+Foc/pa:%>käänpä    #        ;


! shorthands

LEXICON N_BACK_CLIT_OPT
0    #   ;
0   N_BACK_CLIT    ;

LEXICON N_FRONT_CLIT_OPT
0    #   ;
0   N_FRONT_CLIT    ;

LEXICON N_BACK_SINGULARS
0    N_BACK_STRONG_SINGULARS ;
0    N_BACK_WEAK_SINGULARS   ;

LEXICON N_FRONT_SINGULARS
0    N_FRONT_STRONG_SINGULARS ;
0    N_FRONT_WEAK_SINGULARS   ;

LEXICON N_FRONT_PLURALS
0   N_FRONT_STRONG_PLURALS ;
0   N_FRONT_WEAK_PLURALS ;

LEXICON N_BACK_PLURALS
0   N_BACK_STRONG_PLURALS ;
0   N_BACK_WEAK_PLURALS ;

LEXICON N_BACK_POSS_AN_OPT
0    N_BACK_POSS   ;
0    N_BACK_POSS_AN ;
0    N_BACK_CLIT ;
0    #    ;

LEXICON N_FRONT_POSS_ÄN_OPT
0    N_FRONT_POSS   ;
0    N_FRONT_POSS_ÄN  ;
0    N_FRONT_CLIT ;
0    #    ;

LEXICON N_BACK_POSS_EN_OPT
0    N_BACK_POSS   ;
0    N_BACK_POSS_EN ;
0    N_BACK_CLIT ;
0    #    ;

LEXICON N_FRONT_POSS_EN_OPT
0    N_FRONT_POSS   ;
0    N_FRONT_POSS_EN  ;
0    N_FRONT_CLIT ;
0    #    ;

LEXICON N_BACK_POSS_OPT
0    N_BACK_POSS   ;
0    N_BACK_CLIT ;
0    #    ;

LEXICON N_FRONT_POSS_OPT
0    N_FRONT_POSS   ;
0    N_FRONT_CLIT ;
0    #    ;

LEXICON N_BACK_POSS_EN_OBL
0   N_BACK_POSS  ;
0   N_BACK_POSS_EN   ;

LEXICON N_FRONT_POSS_EN_OBL
0   N_FRONT_POSS  ;
0   N_FRONT_POSS_EN   ;

! derivs
LEXICON N_BACK_DERIV_INEN
i   N_AAKKOSELLINEN ;
lai N_AAKKOSELLINEN ;

LEXICON N_FRONT_DERIV_INEN
i   N_KYLKIÄINEN ;
läi N_KYLKIÄINEN ;

LEXICON N_BACK_DERIV_TONTSE
tse             ko-kaan+Adverb  ;
lli             nen-se-soun+ta-en-ia-ien-ten-iin        ;
llisuu          s-te-de-ks-toun+ta-en-ia-ien-iin        ;
sti             ko-kaan+Adverb     ;
t               ar-tare-taroun+ta-en-ia-ien-ten-iin   ;
t               on-toma-tomoun+ta-an-ia-ien-(ten)-iin ;
ttomuu          s-te-de-ks-toun+ta-en-ia-ien-iin     ;

LEXICON N_FRONT_DERIV_TÖNTSE
tse             ko-kaan+Adverb  ;
lli             nen-se-soun+ta-en-ia-ien-ten-iin        ;
llisuu          s-te-de-ks-toun+ta-en-ia-ien-iin        ;
sti             ko-kaan+Adverb     ;
t               ar-tare-taroun+ta-en-ia-ien-ten-iin   ;
t               on-toma-tomoun+ta-an-ia-ien-(ten)-iin ;
ttomuu          s-te-de-ks-toun+ta-en-ia-ien-iin     ;

LEXICON N_BACK_DERIV_ITTAIN
itse    ko-kaan+Adverb     ;
ittain  ko-kaan+Adverb     ;
!
LEXICON N_FRONT_DERIV_ITTÄIN
itse    kö-kään+Adverb     ;
ittäin  kö-kään+Adverb     ;
!
! compoundingT
LEXICON N_COMPOUND_0
!!€ talojuttu
0   N_COMPOUND  ;

LEXICON N_COMPOUND_N
!!€ talonjuttu
0:%>n               N_COMPOUND    ;

LEXICON N_COMPOUND_IEN
!!€ naisienjuttu
0:%>i%>en                 N_COMPOUND  ;

LEXICON N_COMPOUND_IDEN
!!€ lepakoidenjuttu
0:%>i%>den                 N_COMPOUND  ;

LEXICON N_COMPOUND_IN
!!€ vanhempainjuttu
0:%>i%>n                 N_COMPOUND     ;

LEXICON N_COMPOUND_ITTEN
!!€ lepakoittenjuttu
0:%>i%>tten               N_COMPOUND     ;

LEXICON N_COMPOUND_JEN
!!€ talojenjuttu
0:%>j%>en                 N_COMPOUND     ;

LEXICON N_COMPOUND_TEN
!!€ miestenjuttu
0:%>ten                 N_COMPOUND     ;


LEXICON N_COMPOUND
!!€ talojuttu
+Use/Circ#:0#     NOMINAL   ;


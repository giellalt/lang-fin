Multichar_Symbols
@U.NUMCASE.SGNOM@ @U.NUMCASE.SGINE@

!! !!Number transcriptions
!!
!! Transcribing numbers to words in Finnish is not completely trivial, one
!! reason is that numbers in Finnish are written as compounds, regardless of
!! length:
!! 123456 satakaksikymmentäkolmetuhattaneljäsataaviisikymmentäkuusi
!! Another limitation is that inflections can be unmarked in running text,
!! that is digit expression is assumed to agree the case of the phrase it is in,
!! e.g.
!! 27 kaksikymmentäseitsemän
!! 27:lle kahdellekymmenelleseitsemälle
!! but in a phrase: "tarjosin 27 osanottajalle" 27 assumes the allative case
!! without marking and it is preferred grammatical form in good writing.

LEXICON Root
!! The morphotactics related to numbers and their transcriptions is that we
!! need to know the whole digit string to know how the length of whole digit
!! string to know what to start reading, and zeroes are not read out but
!! have an effect to readout.
!! The numerals are systematic and perfectly compositional:
!! the implementation of 100 000–999 999 is almost
!! exactly same as 100 000 000–999 000 000 and everything afterwads with the
!! change of word *tuhat*~*tuhatta*, *miljoona*~*miljoonaa*, *miljardia*,
!! *biljoonaa*, *biljardia* and so forth. The numbers are built from ~single
!! word length blocks in decreasing order with the exception of zig-zagging
!! over numbers 11–19 where the second digit comes before first.
!! The rest of this doc is the lexicons in descendign order of magnitude.
HUNDREDSMRD ;		! 200 000 000 000–999 999 999 999
1:sata HUNDREDMRD ;	! 100 000 000 000-199 999 999 999
TENSMRD ;			!  20 000 000 000– 29 999 999 999
TEENSMRD ;		!      11 000 000 000- 19 999 999 999
TENMRD ;			!  10 000 000 000– 10 999 999 999
ONESMRD ;			!   1 000 000 000–  9 999 999 999
HUNDREDSM ;		!         200 000 000–    999 999 999
1:sata HUNDREDM ;	!     100 000 000–    199 999 999
TENSM ;			!          20 000 000–     99 999 999
TEENSM ;		!          11 000 000–     19 999 999
TENM ;			!          10 000 000–     10 999 999
ONESM ;			!           1 000 000–      9 999 999
HUNDREDST ;		!             200 000–        999 999
1:sata HUNDREDT ;	!         100 000–        199 999
TENST ;			!              20 000–         99 999
TEENST ;		!              11 000–         19 999
TENT ;          !              10 000-         10 999
ONEST ;			!               2 000–          9 999
1:tuhat THOUSAND ;	!           1 000–          1 999
UNDERTHOUSAND ;		!             100–            999
TENS ;			!                  20-             99
TEENS ;			!                  11-             19
JUSTTEN ;       !                                  10
ONES ;			!                   1-              9
ZERO ;          !                                   0

LEXICON HUNDREDSMRD
!! lexicon @LEXNAME@ contains numbers 2-9 that need to be followed by exactly
!! 11 digits: 200 000 000 000–999 999 999 999
!! this is to implement *N*sataa...miljardia...
2:kaksi  CUODIMRD ;
3:kolme CUODIMRD ;
4:neljä CUODIMRD ;
5:viisi CUODIMRD ;
6:kuusi CUODIMRD ;
7:seitsemän CUODIMRD ;
8:kahdeksan CUODIMRD ;
9:yhdeksän CUODIMRD ;

LEXICON CUODIMRD
!! lexicon @LEXNAME@ contains numbers 2-9 that need to be followed by exactly
!! this is to implement N*sataa*...miljardia...
:sataa HUNDREDMRD ;

LEXICON HUNDREDMRD
!! lexicon @LEXNAME@ is for numbers in range: 100 000 000 000–199 000 000 000
!! this is to implement *sata*...miljardia...
TENMRD ;
TENSMRD ;
TEENSMRD ;
%0: ONESMRD ;
%0%0: MILJONRD ;

!LEXICON JUSTTENM
!kymmenen:1%0 LOHKAIM ;

LEXICON TEENSMRD
!! lexicon @LEXNAME@ is for numbers with 11 000 000 000–19 000 000 000
!! this is to implement ...N*toista*...miljardia...
1: TEENMRD ;

LEXICON TEENMRD
!! lexicon @LEXNAME@ is for numbers with 11 000 000 000–19 000 000 000
!! this is to implement ...*N*toista...miljardia...
1:yksi LOHKAIMRD ;
2:kaksi LOHKAIMRD ;
3:kolme LOHKAIMRD ;
4:neljä LOHKAIMRD ;
5:viisi LOHKAIMRD ;
6:kuusi LOHKAIMRD ;
7:seitsemän LOHKAIMRD ;
8:kahdeksan LOHKAIMRD ;
9:yhdeksän LOHKAIMRD ;

LEXICON LOHKAIMRD
:toista MILJARD ;

LEXICON TENSMRD
!! lexicon @LEXNAME@ is for numbers with 20 000 000 000–90 000 000 000
!! this is to implement ...*N*kymmentä...miljardia...
!1:yksi LÅGEVM ;
2:kaksi LÅGEVMRD ;
3:kolme LÅGEVMRD ;
4:neljä LÅGEVMRD ;
5:viisi LÅGEVMRD ;
6:kuusi LÅGEVMRD ;
7:seitsemän LÅGEVMRD ;
8:kahdeksan LÅGEVMRD ;
9:yhdeksän LÅGEVMRD ;

LEXICON TENMRD
!! lexicon @LEXNAME@ is for numbers with 10 000 000 000–10 999 999 999
!! this is to implement ...*kymmenen*miljardia...
1%0:kymmenen MILJARD ;

LEXICON LÅGEVMRD
!! lexicon @LEXNAME@ is for numbers with 20 000 000 000–90 000 000 000
!! this is to implement ...N*kymmentä*...miljardia...
%0:kymmentä MILJARD ;
:kymmentä ONESMRD ;

LEXICON ONESMRD
!! lexicon @LEXNAME@ is for numbers with 1 000 000 000–9 000 000 000
!! this is to implement ...*N*miljardia...
!1:yksi% miljoona%   OVERTHOUSANDS ;
1:miljardi OVERMILLIONS ;
2:kaksi MILJARD ;
3:kolme MILJARD ;
4:neljä MILJARD ;
5:viisi MILJARD ;
6:kuusi MILJARD ;
7:seitsemän MILJARD ;
8:kahdeksan MILJARD ;
9:yhdeksän MILJARD ;

LEXICON MILJARD
!! lexicon @LEXNAME@ is for numbers with 1 000 000 000–9 000 000 000
!! this is to implement ...N*miljardia*...
:miljardia OVERMILLIONS ;

! =================
! Under the milliard
! =================

LEXICON OVERMILLIONS
!! lexicon @LEXNAME@ is for the millions *part* of numbers greater than 1 milliard
HUNDREDSM ;		                ! ¤## ### ###
1:sata HUNDREDM ;	            ! 1## ### ###
%0: TENSM ;		                !  ¤# ### ###
%0: TEENSM ;		            !  1¤ ### ###
%0: TENM ;		                !  10 ### ###
%0%0: ONESM ;		            !   ¤ ### ###
%0%01:tuhat MILJON ;	        !   1 ### ###
%0%0%0: UNDERMILLION ;          !
%0%0%0%0: TENST ;		        !  ¤# ### ###
%0%0%0%0: TEENST ;		        !  1¤ ### ###
%0%0%0%0%0: ONEST ;		        !   ¤ ### ###
%0%0%0%0%01:tuhat THOUSAND ;	!   ¤ ### ###
%0%0%0%0%0%0: UNDERTHOUSAND ;   !
%0%0%0%0%0%0%0: TENS ;          !
%0%0%0%0%0%0%0: TEENS ;         !
%0%0%0%0%0%0%0%0: ONES ;        !
%0%0%0%0%0%0%0%0%0: # ;	        !


LEXICON HUNDREDSM
!! lexicon @LEXNAME@ contains numbers 2-9 that need to be followed by exactly
!! 8 digits: 200 000 000–999 999 999
!! this is to implement *N*sataa...miljoonaa...
2:kaksi  CUODIM ;
3:kolme CUODIM ;
4:neljä CUODIM ;
5:viisi CUODIM ;
6:kuusi CUODIM ;
7:seitsemän CUODIM ;
8:kahdeksan CUODIM ;
9:yhdeksän CUODIM ;

LEXICON CUODIM
!! lexicon @LEXNAME@ contains numbers 2-9 that need to be followed by exactly
!! this is to implement N*sataa*...miljoonaa...
:sataa HUNDREDM ;

LEXICON HUNDREDM
!! lexicon @LEXNAME@ is for numbers in range: 100 000 000–199 000 000
!! this is to implement *sata*...miljoonaa...
TENM ;
TENSM ;
TEENSM ;
%0: ONESM ;
%0%0: MILJON ;

!LEXICON JUSTTENM
!kymmenen:1%0 LOHKAIM ;

LEXICON TEENSM
!! lexicon @LEXNAME@ is for numbers with 11 000 000–19 000 000
!! this is to implement ...N*toista*...miljoonaa...
1: TEENM ;

LEXICON TEENM
!! lexicon @LEXNAME@ is for numbers with 11 000 000–19 000 000
!! this is to implement ...*N*toista...miljoonaa...
1:yksi LOHKAIM ;
2:kaksi LOHKAIM ;
3:kolme LOHKAIM ;
4:neljä LOHKAIM ;
5:viisi LOHKAIM ;
6:kuusi LOHKAIM ;
7:seitsemän LOHKAIM ;
8:kahdeksan LOHKAIM ;
9:yhdeksän LOHKAIM ;

LEXICON LOHKAIM
:toista MILJON ;

LEXICON TENSM
!! lexicon @LEXNAME@ is for numbers with 20 000 000–90 000 000
!! this is to implement ...*N*kymmentä...miljoonaa...
!1:yksi LÅGEVM ;
2:kaksi LÅGEVM ;
3:kolme LÅGEVM ;
4:neljä LÅGEVM ;
5:viisi LÅGEVM ;
6:kuusi LÅGEVM ;
7:seitsemän LÅGEVM ;
8:kahdeksan LÅGEVM ;
9:yhdeksän LÅGEVM ;

LEXICON TENM
!! lexicon @LEXNAME@ is for numbers with 10 000 000–10 999 999
!! this is to implement ...*kymmenen*miljoonaa...
1%0:kymmenen MILJON ;

LEXICON LÅGEVM
!! lexicon @LEXNAME@ is for numbers with 20 000 000–90 000 000
!! this is to implement ...N*kymmentä*...miljoonaa..
%0:kymmentä MILJON ;
:kymmentä ONESM ;

LEXICON ONESM
!! lexicon @LEXNAME@ is for numbers with 1 000 000–9 000 000
!! this is to implement ...*N*miljoonaa...
!1:yksi% miljoona%   OVERTHOUSANDS ;
1:miljoona OVERTHOUSANDS ; ! 1######
2:kaksi MILJON ;     ! 2######
3:kolme MILJON ;     ! 3######
4:neljä MILJON ;     ! 4######
5:viisi MILJON ;     ! 5######
6:kuusi MILJON ;     ! 6######
7:seitsemän MILJON ; ! 7######
8:kahdeksan MILJON ; ! 8######
9:yhdeksän MILJON ;  ! 9######

LEXICON MILJON
!! lexicon @LEXNAME@ is for numbers with 1 000 000–9 000 000
!! this is to implement ...N*miljoonaa*...
:miljoonaa OVERTHOUSANDS ; ! ¤######

! =================
! Under the miljon
! =================

LEXICON UNDERMILLION
!! lexicon @LEXNAME@ is for numbers with 100 000–900 000 after milliards
HUNDREDST ;       !       ¤#####
1:sata HUNDREDT ; !       1#####

LEXICON OVERTHOUSANDS
!! lexicon @LEXNAME@ is for the thousands *part* of numbers greater than 1 million
HUNDREDST ;		!         ¤#####
1:sata HUNDREDT ;	!     1#####
%0: TENST ;		!         0¤####
%0: TEENST ;		!     01####
%0: TENT ;          !     010###
%0%0: ONEST ;		!     00¤###
%0%01:tuhat THOUSAND ;	! 001###
%0%0%0: UNDERTHOUSAND ; ! 000###
%0%0%0%0: TENS ;    !     0000¤#
%0%0%0%0: TEENS ;   !     00001#
%0%0%0%0%0: ONES ;  !     00000#
%0%0%0%0%0%0: # ;	!     000000

LEXICON HUNDREDST
!! lexicon @LEXNAME@ contains numbers 2-9 that need to be followed by exactly
!! 5 digits: 200 000–999 999
!! this is to implement *N*sataa...tuhatta...
2:kaksi  CUODIT ;	! 2#####
3:kolme CUODIT ;	! 3#####
4:neljä CUODIT ;	! 4#####
5:viisi CUODIT ;	! 5#####
6:kuusi CUODIT ;	! 6#####
7:seitsemän CUODIT ;	! 7#####
8:kahdeksan CUODIT ;	! 8#####
9:yhdeksän CUODIT ;	! 9#####

LEXICON CUODIT
!! lexicon @LEXNAME@ contains numbers 2-9 that need to be followed by exactly
!! this is to implement N*sataa*...tuhatta...
:sataa HUNDREDT ;	! ¤#####

LEXICON HUNDREDT	! ¤ = 1-9, # = 0-9
!! lexicon @LEXNAME@ is for numbers in range: 100 000–199 000
!! this is to implement *sata*...tuhatta...
TENST ;			! ¤¤####
TENT ;			! ¤10###
TEENST ;		! ¤1¤###
%01:yksi THOUSANDS ; ! ¤01###
%0: ONEST ;		! ¤0¤###
%0%0: THOUSANDS ;	! ¤00###

LEXICON TEENST
!! lexicon @LEXNAME@ is for numbers with 11 000–19 000
!! this is to implement ...N*toista*...tuhatta...
1: TEENT ;  ! 11###

!LEXICON JUSTTENT
!kymmenen% tuhatta:1%0 THOUSANDS ;

LEXICON TEENT
!! lexicon @LEXNAME@ is for numbers with 11 000–19 000
!! this is to implement ...*N*toista...tuhatta...
1:yksi LOHKAIT ;      ! 11###
2:kaksi LOHKAIT ;     ! 12###
3:kolme LOHKAIT ;     ! 13###
4:neljä LOHKAIT ;     ! 14###
5:viisi LOHKAIT ;     ! 15###
6:kuusi LOHKAIT ;     ! 16###
7:seitsemän LOHKAIT ; ! 17###
8:kahdeksan LOHKAIT ; ! 18###
9:yhdeksän LOHKAIT ;  ! 19###

LEXICON LOHKAIT
:toista THOUSANDS ;  ! 11###-19###

LEXICON TENST
!! lexicon @LEXNAME@ is for numbers with 20 000–90 000
!! this is to implement ...*N*kymmentä...tuhatta...
2:kaksi LÅGEVT ;     ! 2####
3:kolme LÅGEVT ;     ! 3####
4:neljä LÅGEVT ;     ! 4####
5:viisi LÅGEVT ;     ! 5####
6:kuusi LÅGEVT ;     ! 6####
7:seitsemän LÅGEVT ; ! 7####
8:kahdeksan LÅGEVT ; ! 8####
9:yhdeksän LÅGEVT ;  ! 9####


LEXICON TENT
!! lexicon @LEXNAME@ is for numbers with 10 000 000–10 999 999
!! this is to implement ...*kymmenen*tuhatta...
1%0:kymmenentuhatta THOUSAND ;  ! 10###

LEXICON LÅGEVT
!! lexicon @LEXNAME@ is for numbers with 20 000–90 000
!! this is to implement ...N*kymmentä*...tuhatta..
%0:kymmentä THOUSANDS ; ! ¤0###
 1:kymmentäyksituhatta THOUSAND ; ! ¤1###
  :kymmentä ONEST ;     ! ¤¤###-¤¤###

LEXICON ONEST
!! lexicon @LEXNAME@ is for numbers with 1 000–9 000
!! this is to implement ...*N*tuhatta...
2:kaksi THOUSANDS ;     ! 2###
3:kolme THOUSANDS ;     ! 3###
4:neljä THOUSANDS ;     ! 4###
5:viisi THOUSANDS ;     ! 5###
6:kuusi THOUSANDS ;     ! 6###
7:seitsemän THOUSANDS ; ! 7###
8:kahdeksan THOUSANDS ; ! 8###
9:yhdeksän THOUSANDS ;  ! 9###

LEXICON THOUSANDS      ! x > 1
!! lexicon @LEXNAME@ is for numbers with 1 000–9 000
!! this is to implement ...N*tuhatta*...
!!€gt-norm:  Thousands
!!€  2000: kaksituhatta
!!€  3456: kolmetuhattaneljäsataaviisikymmentäkuusi
:tuhatta THOUSAND ;    ! ¤###

LEXICON THOUSAND
!! lexicon @LEXNAME@ is for the ones-tens-hundreds of numbers greater than thousand
!HUNDREDS ;		!         ¤#####
UNDERTHOUSAND ;	   ! 1¤##
%0: TENS ;	       ! 10¤#
%0: TEENS ;	       ! 101¤
%0%0: ONES ;	   ! 100¤
%0%0%0: # ;	       ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
!! lexicon @LEXNAME@ is for numbers with 100–900 after thousands
HUNDREDS ;           ! 2##-9##
1:sata HUNDRED ;     ! 1##

LEXICON HUNDREDS
!! lexicon @LEXNAME@ contains numbers 2-9 that need to be followed by exactly
!! 2 digits: 200–999
!! this is to implement *N*sataa...
2:kaksi  CUODI ;     ! 2##
3:kolme CUODI ;      ! 3##
4:neljä CUODI ;      ! 4##
5:viisi CUODI ;      ! 5##
6:kuusi CUODI ;      ! 6##
7:seitsemän CUODI ;  ! 7##
8:kahdeksan CUODI ;  ! 8##
9:yhdeksän CUODI ;   ! 9##

LEXICON CUODI
!! lexicon @LEXNAME@ contains numbers 2-9 that need to be followed by exactly
!! this is to implement N*sataa*...
!!€gt-norm:  Hundreds
!!€  200: kaksisataa
!!€  345: kolmesataaneljäkymmentäviisi
:sataa HUNDRED ;     ! ¤##

LEXICON HUNDRED
!! lexicon @LEXNAME@ is for numbers in range: 100–999
TENS ;               ! #¤#
TEENS ;              ! #1#
%0: ONES ;           ! #0#
%0%0: # ;            ! #00

LEXICON TEENS
!! lexicon @LEXNAME@ is for numbers with 11–19
!! this is to implement ...N*toista*
1: TEEN ;            ! 1¤

LEXICON TEEN
!! lexicon @LEXNAME@ is for numbers with 11–19
!! this is to implement ...*N*toista
!!€gt-norm:  Teens
!!€  11: yksitoista
!!€  12: kaksitoista
!!€  13: kolmetoista
1:yksi LOHKAI ;      ! 11
2:kaksi LOHKAI ;     ! 12
3:kolme LOHKAI ;     ! 13
4:neljä LOHKAI ;     ! 14
5:viisi LOHKAI ;     ! 15
6:kuusi LOHKAI ;     ! 16
7:seitsemän LOHKAI ; ! 17
8:kahdeksan LOHKAI ; ! 18
9:yhdeksän LOHKAI ;  ! 19

LEXICON LOHKAI
:toista # ;

!LEXICON
!kymmentä:1%0 # ;

LEXICON TENS
!! lexicon @LEXNAME@ is for numbers with 20–90
!! this is to implement ...*N*kymmentä...
JUSTTEN ;            ! 10
2:kaksi LÅGEV ;      ! 2#
3:kolme LÅGEV ;      ! 3#
4:neljä LÅGEV ;      ! 4#
5:viisi LÅGEV ;      ! 5#
6:kuusi LÅGEV ;      ! 6#
7:seitsemän LÅGEV ;  ! 7#
8:kahdeksan LÅGEV ;  ! 8#
9:yhdeksän LÅGEV ;   ! 9#

LEXICON LÅGEV
!! lexicon @LEXNAME@ is for numbers with 20–90
!! this is to implement ...N*kymmentä*...
!!€gt-norm:  Tens
!!€  20: kaksikymmentä
!!€  34: kolmekymmentäneljä
%0:kymmentä # ;      ! ¤0
  :kymmentä ONES ;   ! ¤#

LEXICON JUSTTEN
!! lexicon @LEXNAME@ is for number 10
!! this is to implement ...*kymmenen*
!!€gt-norm:  Ten
!!€  1: kymmenen
1%0:kymmenen # ;     ! 10

LEXICON ONES
!! lexicon @LEXNAME@ is for numbers with 1–9
!! this is to implement yksi, kaksi, kolme..., yhdeksän
!!€gt-norm:  Ones
!!€  1: yksi
!!€  2: kaksi
!!€  3: kolme
1:yksi # ;           ! 1
2:kaksi # ;          ! 2
3:kolme # ;          ! 3
4:neljä # ;          ! 4
5:viisi # ;          ! 5
6:kuusi # ;          ! 6
7:seitsemän # ;      ! 7
8:kahdeksan # ;      ! 8
9:yhdeksän # ;       ! 9

LEXICON ZERO
!! lexicon @LEXNAME@ is for number 0
!! nolla
!!€gt-norm:  Zero
!!€  0: nolla
%0:nolla # ;         ! 0
